       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID                          EX007P28.
       AUTHOR                              LUCAS.
      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT MOVPRDD4 ASSIGN            TO UT-S-MOVPRDD4
           FILE STATUS FS-MOVPRDD4
           .
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       FILE                                SECTION.
      *----------------------------------------------------------------*
       FD  MOVPRDD4
           RECORDING MODE IS F
           .
           COPY MOVPRDD4 REPLACING ==::== BY ====.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
      *  BOOK VARIAVEIS ESPELHO
      *----------------------------------------------------------------*
           COPY MOVPRDD4 REPLACING ==::== BY ==WS-==.
      *----------------------------------------------------------------*
      *  BOOKS DATA E TEMPO
      *----------------------------------------------------------------*
           COPY VARDATA.
           COPY VARPROC.
      *----------------------------------------------------------------*
      *  BOOKS VARIAVEIS HOSPEDEIRAS
      *----------------------------------------------------------------*
           EXEC SQL
              INCLUDE BK002TP
           END-EXEC.
      *----------------------------------------------------------------*
      *  INDICADORES DA TABELA PRODUTO
      *----------------------------------------------------------------*
       01  INDICADORES.
           05 IUNIDPROD                    PIC S9(4) COMP.
           05 ILOCALPROD                   PIC S9(4) COMP.
           05 IQTDEST                      PIC S9(4) COMP.
           05 IQTDMAX                      PIC S9(4) COMP.
           05 IQTDMIN                      PIC S9(4) COMP.
           05 IPRECOCOMPRA                 PIC S9(4) COMP.
           05 IPRECOVENDA                  PIC S9(4) COMP.
           05 IPERCOMIS                    PIC S9(4) COMP.
      *----------------------------------------------------------------*
      *  BOOKS VARIAVEIS PARA O SQL
      *----------------------------------------------------------------*
           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.
      *----------------------------------------------------------------*
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(05) COMP.
           05 WS-CTLIDO-PROD               PIC 9(05) COMP.
           05 WS-CTLIDO-GERAL              PIC 9(05) COMP.
           05 WS-CTEXC                     PIC 9(05) COMP.
           05 WS-CTALT                     PIC 9(05) COMP.
           05 WS-CTINV                     PIC 9(05) COMP.
      *
       01  WS-CONTADORES-FORMATADOS.
           05 WS-CTLIDO-F                  PIC ZZ.ZZ9.
           05 WS-CTLIDO-PROD-F             PIC ZZ.ZZ9.
           05 WS-CTLIDO-GERAL-F            PIC ZZ.ZZ9.
           05 WS-CTEXC-F                   PIC ZZ.ZZ9.
           05 WS-CTALT-F                   PIC ZZ.ZZ9.
           05 WS-CTINV-F                   PIC ZZ.ZZ9.
      *----------------------------------------------------------------*
      *  FLAGS PARA FILE STATUS
      *----------------------------------------------------------------*
       01  FS-MOVPRDD4                      PIC X(02).
           88 SUCESSO-MOV                           VALUE "00".
           88 FIM-ARQ-MOV                           VALUE "10".
      *----------------------------------------------------------------*
      *  DECLARACAO DE VARIAVEIS
      *----------------------------------------------------------------*
      *87  WS-SALDOCLI-F                   PIC ZZ.ZZ9,99.
       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).
       77  WS-SQLCODE                      PIC +9(09).
      *----------------------------------------------------------------*
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
      *0000-EXEMANUT.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL FIM-ARQ-MOV
           PERFORM 3000-TERMINO
           STOP RUN
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
           INITIALIZE WS-CONTADORES-COMP
      *
           OPEN INPUT MOVPRDD4
           IF NOT SUCESSO-MOV
              MOVE "ERRO ABERTURA MOV"     TO WS-MSG
              MOVE FS-MOVPRDD4             TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           EXEC SQL
               DECLARE MANUTENCAO CURSOR WITH HOLD FOR
                   SELECT  CODPROD
                         , DESCPROD
                         , UNIDPROD
                         , LOCALPROD
                         , QTDEST
                         , QTDMAX
                         , QTDMIN
                         , PRECOCOMPRA
                         , PRECOVENDA
                         , PERCOMIS
                    FROM TBPRODUTO
                    WHERE QTDEST < QTDMIN
                    FOR UPDATE OF   CODPROD
                                   , DESCPROD
                                   , UNIDPROD
                                   , LOCALPROD
                                   , QTDEST
                                   , QTDMAX
                                   , QTDMIN
                                   , PRECOCOMPRA
                                   , PRECOVENDA
                                   , PERCOMIS
           END-EXEC
           PERFORM 1100-LER-MOVPRDD4
           .
      *
       1100-LER-MOVPRDD4.
           READ MOVPRDD4 INTO WS-REG-MOVPRDD4
           IF SUCESSO-MOV
              ADD 1                        TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-MOV
                 MOVE "ERRO LEITURA MOV"    TO WS-MSG
                 MOVE FS-MOVPRDD4           TO WS-FS
                 PERFORM 9000-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           IF WS-TIPOMOVTO-D4 = 'A' OR 'E'
      *   ABERTURA DO CURSOR
              EXEC SQL
                 OPEN MANUTENCAO
              END-EXEC
      *
              IF SQLCODE NOT EQUAL ZERO
                 MOVE "ERRO AO ABRIR  CURSOR" TO WS-MSG
                 MOVE SQLCODE                 TO WS-SQLCODE
                 GO TO 9000-ERRO-DB2
              END-IF
      *
              PERFORM 2100-LER-MANUTENCAO WITH TEST AFTER
              UNTIL WS-CODPROD-D4 = CODPROD OR SQLCODE = +100
      *
              IF SQLCODE = 0
                 DISPLAY 'PARA LOCALIZAR PRODUTO ' DESCPROD
                       'FORAM NECESSARIAS LER' WS-CTLIDO-PROD
                       'REGISTROS DO CURSOR'
                 MOVE 0                    TO WS-CTLIDO-PROD
      *
                 EVALUATE WS-TIPOMOVTO-D4
                    WHEN 'E' PERFORM 2200-EXCLUSAO
                    WHEN 'A' PERFORM 2300-ALTERACAO
                 END-EVALUATE
              ELSE
                   ADD 1 TO WS-CTINV
              END-IF
      *
              EXEC SQL
                  CLOSE MANUTENCAO
              END-EXEC
      *
              IF SQLCODE NOT EQUAL ZERO
                 MOVE "ERRO AO FECHAR CURSOR" TO WS-MSG
                 MOVE SQLCODE                 TO WS-SQLCODE
                 GO TO 9000-ERRO-DB2
              END-IF
           ELSE
               ADD 1 TO WS-CTINV
           END-IF
      *
           PERFORM 1100-LER-MOVPRDD4
           .
      *----------------------------------------------------------------*
      *  MOVER DADOS DA VARIAVEL ESPELHO PARA A HOSPEDEIRA
      *----------------------------------------------------------------*
       2100-LER-MANUTENCAO.
           EXEC SQL
                FETCH MANUTENCAO
                INTO  :CODPROD
                     ,:DESCPROD
                     ,:UNIDPROD       :IUNIDPROD
                     ,:LOCALPROD      :ILOCALPROD
                     ,:QTDEST         :IQTDEST
                     ,:QTDMAX         :IQTDMAX
                     ,:QTDMIN         :IQTDMIN
                     ,:PRECOCOMPRA    :IPRECOCOMPRA
                     ,:PRECOVENDA     :IPRECOVENDA
                     ,:PERCOMIS       :IPERCOMIS
           END-EXEC
           IF SQLCODE = 0
              PERFORM 9000-TRATA-INDICADOR
              ADD 1                        TO WS-CTLIDO-GERAL
              ADD 1                        TO WS-CTLIDO-PROD
           ELSE
              IF SQLCODE NOT = +100
                 MOVE "ERRO LEITURA CURSOR MANUTENCAO"
                                            TO WS-MSG
                 MOVE SQLCODE               TO WS-SQLCODE
                 GO TO 9000-ERRO-DB2
              END-IF
           END-IF
           .
      *
       2200-EXCLUSAO.
           EXEC SQL
              DELETE FROM TBPRODUTO
              WHERE CURRENT OF MANUTENCAO
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0    ADD 1 TO WS-CTEXC
                         EXEC SQL COMMIT END-EXEC
              WHEN +100 ADD 1 TO WS-CTINV
              WHEN OTHER
                   MOVE 'ERRO DELETE NA TABELA TBPRODUTO'
                                          TO WS-MSG
                   MOVE SQLCODE           TO WS-SQLCODE
                   PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       2300-ALTERACAO.
           MOVE WS-CODPROD-D4             TO CODPROD
      *
      * TESTE DA VARIAVEL ESPELHO
           IF WS-DESCPROD-D4 NOT = SPACES
              MOVE +20                     TO DESCPROD-LEN
              MOVE WS-DESCPROD-D4          TO DESCPROD-TEXT
           END-IF
      *
           IF WS-UNIDPROD-D4  NOT = SPACES
              MOVE WS-UNIDPROD-D4          TO UNIDPROD
           END-IF
      *
           IF WS-LOCALPROD-D4 NOT = SPACES
              MOVE WS-LOCALPROD-D4         TO LOCALPROD
           END-IF
      *
           IF WS-QTDEST-D4 IS NUMERIC
              MOVE WS-QTDEST-D4            TO QTDEST
           END-IF
      *
           IF WS-QTDMAX-D4 IS NUMERIC
              MOVE WS-QTDMAX-D4            TO QTDMAX
           END-IF
      *
           IF WS-QTDMIN-D4 IS NUMERIC
              MOVE WS-QTDMIN-D4            TO QTDMIN
           END-IF
      *
           IF WS-PRECOCOMPRA-D4 IS NUMERIC
              MOVE WS-PRECOCOMPRA-D4            TO PRECOCOMPRA
           END-IF
      *
           IF WS-PRECOVENDA-D4 IS NUMERIC
              MOVE WS-PRECOVENDA-D4            TO PRECOVENDA
           END-IF
      *
           IF WS-PERCOMIS-D4 IS NUMERIC
              MOVE WS-PERCOMIS-D4              TO PERCOMIS
           END-IF
      *
           EXEC SQL
              UPDATE TBPRODUTO
              SET    DESCPROD     = :DESCPROD
                  ,  UNIDPROD     = :UNIDPROD
                  ,  LOCALPROD    = :LOCALPROD
                  ,  QTDEST       = :QTDEST
                  ,  QTDMAX       = :QTDMAX
                  ,  QTDMIN       = :QTDMIN
                  ,  PRECOCOMPRA  = :PRECOCOMPRA
                  ,  PRECOVENDA   = :PRECOVENDA
                  ,  PERCOMIS     = :PERCOMIS
              WHERE CURRENT OF MANUTENCAO
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0 ADD 1 TO WS-CTALT
                     EXEC SQL COMMIT END-EXEC
              WHEN +100 ADD 1 TO WS-CTINV
              WHEN -545 ADD 1 TO WS-CTINV
              WHEN OTHER
                   MOVE 'ERRO UPDATE NA TABELA TBPRODUTO'
                                           TO WS-MSG
                   MOVE SQLCODE            TO WS-SQLCODE
                   PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       3000-TERMINO.
      *
           CLOSE MOVPRDD4
           IF NOT SUCESSO-MOV
              MOVE "ERRO FECHAMENTO"       TO WS-MSG
              MOVE FS-MOVPRDD4             TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
           PERFORM 9000-CALCULA-TEMPO-PROC
      *
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTLIDO-PROD             TO WS-CTLIDO-PROD-F
           MOVE WS-CTLIDO-GERAL            TO WS-CTLIDO-GERAL-F
           MOVE WS-CTEXC                   TO WS-CTEXC-F
           MOVE WS-CTINV                   TO WS-CTINV-F
           MOVE WS-CTALT                   TO WS-CTALT-F
      *
           PERFORM 9000-IMPRIME-DATA
      *
           DISPLAY "   "
           DISPLAY "   "
           DISPLAY "=================================================="
           DISPLAY "==      ESTATISTICA FINAL DE PROCESSAMENTO      =="
           DISPLAY "=================================================="
           DISPLAY "QTDE. MOVIMENTOS LIDO MOVPRDD4: " WS-CTLIDO-F
           DISPLAY "PRODUTOS LIDOS NO CURSOR PROD.: " WS-CTLIDO-PROD-F
           DISPLAY "PRODUTOS LIDOS NO CURSOR GERAL: " WS-CTLIDO-GERAL-F
           DISPLAY "QTDE. PRODUTOS EXCLUIDOS..........: " WS-CTEXC-F
           DISPLAY "QTDE. PRODUTOS ALTERADOS..........: " WS-CTALT-F
           DISPLAY "QTDE. PRODUTOS INVALIDOS..........: " WS-CTINV-F
           DISPLAY "=================================================="
           DISPLAY "TEMPO TOTAL DE PROCESSAMENTO......: " WS-DIFERENCA
           DISPLAY "=================================================="
           DISPLAY "=================================================="
           DISPLAY "         TERMINO NORMAL EX007P28                  "
           DISPLAY "=================================================="
           .
      *
       9000-TRATA-INDICADOR.
      *     TESTE DOS INDICADORES
           IF IUNIDPROD LESS ZERO
              MOVE SPACES                  TO UNIDPROD
           END-IF
      *
           IF ILOCALPROD LESS ZERO
              MOVE SPACES                  TO LOCALPROD
           END-IF
      *
           IF IQTDEST LESS ZERO
              MOVE ZEROS                   TO QTDEST
           END-IF
      *
           IF IQTDMAX LESS ZERO
              MOVE ZEROS                   TO QTDMAX
           END-IF
      *
           IF IQTDMIN LESS ZERO
              MOVE ZEROS                   TO QTDMIN
           END-IF
      *
           IF IPRECOCOMPRA LESS ZERO
              MOVE ZEROS                   TO PRECOCOMPRA
           END-IF
      *
           IF IPRECOVENDA LESS ZERO
              MOVE ZEROS                   TO PRECOVENDA
           END-IF
      *
           IF IPERCOMIS LESS ZERO
              MOVE ZEROS                   TO PERCOMIS
           END-IF
           .
      *
       9000-ERRO-DB2.
           DISPLAY "---------------------------------------"
           DISPLAY "MENSAGEM....: " WS-MSG
           DISPLAY "FILE STATUS.: " WS-FS
           DISPLAY "---------------------------------------"
           DISPLAY "--  TERMINO ANORMAL DO PROGRAMA      --"
           DISPLAY "---------------------------------------"
           MOVE 16                 TO RETURN-CODE
           STOP RUN
           .
      *
           COPY ROTERRO.
           COPY ROTPROC.
           COPY ROTDATA.
