      *--------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *--------------------------------------------------------------*
       PROGRAM-ID.                         FS07PGMH.
       AUTHOR.                             LUCAS.

      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *--------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.
       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
      *
       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).
      *
           COPY FS07CAD.
           COPY DFHAID.
           COPY DFHBMSCA.
      *
           EXEC SQL
               INCLUDE BK002TP
           END-EXEC.

           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.
      * VARIAVEL ESPELHO DO VCADPRD
       01  WS-REG-VCADPRD.
           05 WS-CODPROD                  PIC X(04).
           05 WS-DESCPROD                 PIC X(20).
           05 WS-UNIDPROD                 PIC X(05).
           05 WS-LOCALPROD                PIC X(05).
           05 WS-QTDEST                   PIC 9(05).
           05 WS-QTDMAX                   PIC 9(05).
           05 WS-QTDMIN                   PIC 9(05).
           05 WS-PRECOCOMPRA              PIC 9(06)V99.
           05 WS-PRECOVENDA               PIC 9(06)V99.
           05 WS-PERCOMIS                 PIC 9(02)V99.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24576 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*
           EXEC CICS HANDLE CONDITION
              MAPFAIL (999-ERRO-MAPA)
              ERROR   (999-ERRO-GENERICO)
           END-EXEC
      *
      * SELETOR DE FASE - O MENU PRINCIPAL POSSUI 3 FASES
      *    FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *    FASE 2 - TRATA O CAMPO T2COD
      *    FASE 3 - TRATA O CAMPO T2CONF
      *---------------------------------------------------------------*A
           MOVE DFHCOMMAREA                 TO WS-DFHCOMMAREA
      *
           IF EIBCALEN EQUAL 0
              MOVE +80                   TO WS-LENGTH
              MOVE 'ENTRE COM A TRANSACAO F07M'
                                         TO WS-MSG-ERRO
           END-IF
      *
           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN '3' PERFORM 300-FASE3
              WHEN OTHER
                 MOVE +80                   TO WS-LENGTH
                 MOVE 'UTILIZE A OPCAO A DA TRANSACAO F07M'
                                            TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .
      *
       100-FASE1.
      * LIMPAR AS VARIAVEIS DO MAPA
           MOVE LOW-VALUES                 TO MAPACADO
      * POSICIONAMENTO LOGICO - DIRETO NO CAMPO DESEJADO
           MOVE -1                         TO T2CODL
      * MOVER A MENSAGEM PARA O CAMPO APROPRIADO
           MOVE 'ENTRE O CODIGO DO PRODUTO PARA CONSULTAR'
                                           TO T2MSGO
      * PROTEGE A DESPROTEGE OS CAMPOS
           PERFORM 999-PROTECAO-FASE2
      * MANDAR A TELA PARA O TERMINAL
           PERFORM 999-MANDA-TELA
      * ENCERRA A TRANSACAO, CHAMANDO A PROXIMA FASE
           PERFORM 999-CHAMA-FASE2
           .
      *
       200-FASE2.
           EXEC CICS HANDLE AID
                ENTER  (210-ENTER)
                PF3    (220-PF3)
                PF5    (230-PF5)
                CLEAR  (230-PF5)
                ANYKEY (240-ANYKEY)
           END-EXEC
      *
           EXEC CICS RECEIVE
                MAP    ('MAPACAD')
                MAPSET ('FS07CAD')
                INTO   (MAPACADI)
           END-EXEC
           .
      *
       210-ENTER.
           IF T2CODL EQUAL 0 OR T2CODI EQUAL SPACES
              MOVE 'CODIGO NAO INFORMADO - TENTE NOVAMENTE'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE2
           END-IF
      *
           MOVE T2CODO                     TO CODPROD
      *
           EXEC SQL
              SELECT
                    CODPROD
                   ,DESCPROD
                   ,UNIDPROD
                   ,LOCALPROD
                   ,QTDEST
                   ,QTDMAX
                   ,QTDMIN
                   ,PRECOCOMPRA
                   ,PRECOVENDA
                   ,PERCOMIS
              INTO  :CODPROD
                   ,:DESCPROD
                   ,:UNIDPROD
                   ,:LOCALPROD
                   ,:QTDEST
                   ,:QTDMAX
                   ,:QTDMIN
                   ,:PRECOCOMPRA
                   ,:PRECOVENDA
                   ,:PERCOMIS
              FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
            END-EXEC
      *
            IF SQLCODE = 100
               PERFORM 999-NOTFND
            END-IF
      *
           MOVE CODPROD                    TO T2CODO
           MOVE DESCPROD-TEXT              TO T2DESCO
           MOVE UNIDPROD                   TO T2UNIDO
           MOVE LOCALPROD                  TO T2LOCALO
           MOVE QTDEST                     TO T2ESTO
           MOVE QTDMAX                     TO T2MAXO
           MOVE QTDMIN                     TO T2MINO
           MOVE PRECOCOMPRA                TO T2COMPO
           MOVE PRECOVENDA                 TO T2VENDAO
           MOVE PERCOMIS                   TO T2COMISO
      *
           PERFORM 999-TRATA-FASE3
           .
      *
       220-PF3.
           MOVE 1                          TO WS-FASE
      *
           EXEC CICS XCTL
              PROGRAM ('FS07PGMM')
              COMMAREA (WS-DFHCOMMAREA)
              LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *
       230-PF5.
           PERFORM 999-CHAMA-FASE1
           .
      *
       240-ANYKEY.
           MOVE 'TECLA INVALIDA'           TO T2MSGO
           PERFORM 999-TRATA-FASE2
           .
      *
        300-FASE3.
            EXEC CICS HANDLE AID
                 ENTER  (310-ENTER)
                 PF3    (320-PF3)
                 PF5    (330-PF5)
                 CLEAR  (330-PF5)
                 ANYKEY (340-ANYKEY)
            END-EXEC
      *
            EXEC CICS RECEIVE
                 MAP    ('MAPACAD')
                 MAPSET ('FS07CAD')
                 INTO   (MAPACADI)
            END-EXEC
            .
      *
       310-ENTER.
           IF T2CONFL EQUAL 0 OR T2CONFI NOT = 'S' AND 'N'
              MOVE 'DIGITE APENAS S OU N'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE3
           END-IF
      *
           MOVE T2CODO                     TO CODPROD
           IF T2CONFI = 'S'
              EXEC SQL
                  DELETE FROM TBPRODUTO
                  WHERE CODPROD = :CODPROD
              END-EXEC
      *
              EXEC SQL
                  COMMIT
              END-EXEC
      *
      *       PERFORM 100-FASE1
           END-IF
      *
           PERFORM 320-PF3
           .
      *
       320-PF3.
           MOVE 1                          TO WS-FASE
      *
           EXEC CICS XCTL
              PROGRAM ('FS07PGMM')
              COMMAREA (WS-DFHCOMMAREA)
              LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *
       330-PF5.
           PERFORM 999-CHAMA-FASE1
           .
      *
       340-ANYKEY.
           MOVE 'TECLA INVALIDA'           TO T2MSGO
           PERFORM 999-TRATA-FASE3
           .
      *
       999-ENCERRA-TRANSACAO.
           EXEC CICS SEND TEXT
              FROM (WS-MSG-ERRO)
              LENGTH (+80)
              ERASE FREEKB ALARM
           END-EXEC
      *
           EXEC CICS RETURN
           END-EXEC
           .
      *
       999-PROTECAO-FASE2.
      * USAR OS SEGUINTES MNEMONICOS DA DFHBMSCA:
      *     DFHBMPRF - (PROT, ALFA, NORM, ON) - PARA PROTEGER
      *     DFHUNIMD - (UNPROT, ALFA, BRT, ON) - PARA DESPROTEGER ALFA
           MOVE DFHUNIMD                      TO T2CODA
      *     DFHUNINT - (UNPROT, NUM, BRT, ON) - PARA DESPROTEGER NUM
           MOVE DFHBMPRF                      TO T2DESCA
           MOVE DFHBMPRF                      TO T2UNIDA
           MOVE DFHBMPRF                      TO T2LOCALA
           MOVE DFHBMPRF                      TO T2ESTA
           MOVE DFHBMPRF                      TO T2MAXA
           MOVE DFHBMPRF                      TO T2MINA
           MOVE DFHBMPRF                      TO T2COMPA
           MOVE DFHBMPRF                      TO T2VENDAA
           MOVE DFHBMPRF                      TO T2COMISA
           MOVE DFHBMPRF                      TO T2CONFA
           .
      *
       999-PROTECAO-FASE3.
      * USAR OS SEGUINTES MNEMONICOS DA DFHBMSCA:
      *     DFHBMPRF - (PROT, ALFA, NORM, ON) - PARA PROTEGER
           MOVE DFHUNIMD                      TO T2CONFA
      *     DFHUNIMD - (UNPROT, ALFA, BRT, ON) - PARA DESPROTEGER ALFA
           MOVE DFHBMPRF                      TO T2CODA
           MOVE DFHBMPRF                      TO T2DESCA
           MOVE DFHBMPRF                      TO T2UNIDA
           MOVE DFHBMPRF                      TO T2LOCALA
           MOVE DFHBMPRF                      TO T2ESTA
           MOVE DFHBMPRF                      TO T2MAXA
           MOVE DFHBMPRF                      TO T2MINA
           MOVE DFHBMPRF                      TO T2COMPA
           MOVE DFHBMPRF                      TO T2VENDAA
           MOVE DFHBMPRF                      TO T2COMISA
           .
      *
       999-MANDA-TELA.
           MOVE EIBTRMID                   TO T2TERMO
           MOVE EIBTRNID                   TO T2TRANO
           MOVE EIBTASKN                   TO T2TASKO
           MOVE WS-FASE                    TO T2FASEO
      *
           EXEC CICS ASSIGN
              USERID(T2USERO)
           END-EXEC
      *
           EXEC CICS LINK
              PROGRAM('AUXCICS1')
              COMMAREA(WS-VAR-TEMPO)
              LENGTH(+18)
           END-EXEC
      *
           MOVE WS-DATA                     TO T2DATAO
           MOVE WS-HORARIO                  TO T2HORAO
      *
           EXEC CICS SEND
              MAP    ('MAPACAD')
              MAPSET ('FS07CAD')
              FROM   (MAPACADO)
              ERASE FREEKB ALARM CURSOR
           END-EXEC
           .
      *
       999-CHAMA-FASE1.
           MOVE '1'                         TO WS-FASE
      *
           EXEC CICS XCTL
              PROGRAM ('FS07PGMH')
              COMMAREA (WS-DFHCOMMAREA)
              LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *
       999-CHAMA-FASE2.
           MOVE '2'                         TO WS-FASE
      *
           EXEC CICS RETURN
              TRANSID ('F07H')
              COMMAREA (WS-DFHCOMMAREA)
              LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *
       999-CHAMA-FASE3.
           MOVE '3'                         TO WS-FASE

           EXEC CICS RETURN
              TRANSID ('F07H')
              COMMAREA (WS-DFHCOMMAREA)
              LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *
       999-TRATA-FASE2.
           MOVE -1                           TO T2CODL
           PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .
      *
       999-TRATA-FASE3.
           MOVE -1                           TO T2CONFL
           MOVE 'CONFIRMA A EXCLUSAO?(S/N)?'
                                             TO T2ACAOO
      * MOVER OS CAMPOS DO REGISTRO PARA A TELA
           PERFORM 999-PROTECAO-FASE3
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE3
           .
      *
       999-ERRO-MAPA.
           MOVE +80                          TO WS-LENGTH
           MOVE 'ERRO MAPA FS07MNU'          TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
      *
       999-NOTFND.
           STRING 'PRODUTO '                 DELIMITED BY SIZE
                   T2CODI                    DELIMITED BY SPACE
                   'NAO LOCALIZADO'          DELIMITED BY SIZE
                                             INTO T2MSGO
           PERFORM 999-TRATA-FASE2
           .
      *
       999-ERRO-GENERICO.
           MOVE 'ERRO GENERICO FS07PGMH'     TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
