       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID                          EX007P16.
       AUTHOR                              LUCAS.
      *----------------------------------------------------------------*
       ENVIRONMENT                        DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT ESTOQUE ASSIGN             TO UT-S-ESTOQUE
           FILE STATUS FS-ESTOQUE
           .
           SELECT RELATO ASSIGN             TO UT-S-RELATO
           FILE STATUS FS-RELATO
           .
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       FILE                                SECTION.
      *----------------------------------------------------------------*
       FD  ESTOQUE
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 45 CHARACTERS
           DATA RECORD IS REG-ESTOQUE
           .
       01  REG-ESTOQUE.
           05 CODPROD-E                     PIC 9(05).
           05 DESCRICAO-E                   PIC X(25).
           05 QTDEST-E                      PIC 9(03).
           05 QTDMIN-E                      PIC 9(03).
           05 QTDMAX-E                      PIC 9(03).
           05 PRECO-E                       PIC 9(04)V99.
      *
       FD  RELATO
           LABEL RECORD OMITTED
           RECORDING MODE IS F
           RECORD CONTAINS 80 CHARACTERS
           DATA RECORD IS REG-RELATO
           .
       01  REG-RELATO                    PIC X(80).
      *----------------------------------------------------------*
       WORKING-STORAGE                  SECTION.
      *----------------------------------------------------------*
       01  WS-CABEC1.
           05 WS-DATA-CABEC1             PIC X(10).
           05 FILLER                     PIC X(02)
                                                VALUE SPACES.
           05 WS-HORA-CABEC1             PIC X(08).
           05 FILLER                     PIC X(02)
                                                 VALUE SPACES.
           05 FILLER                     PIC X(45)
                                                 VALUE
                   "RELACAO DE PRODUTOS PARA REPOSICAO DE ESTOQUE".
           05 FILLER                     PIC X(11)
                                                 VALUE
                                                     " PAG. ".
           05 WS-PAG-CABEC1 PIC Z9.
       01  WS-CABEC2                     PIC X(80)
                                                 VALUE ALL "-".
       01  WS-CABEC3.
           05 FILLER                     PIC X(40)
                                                 VALUE
                                           " CODIGO DESCRICAO Q".
           05 FILLER                     PIC X(40)
                                                 VALUE
                                 "T.EST. QT.MIN. QT.REP. ULT.PRECO ".
       01  WS-CABEC4.
           05 FILLER                    PIC X(40)
                                                VALUE
              "  ------   -------------------------   -".
           05 FILLER                    PIC X(40)
                                                VALUE
              "------   -------   -------   ---------  ".
       01  WS-LINDET.
           05 FILLER                    PIC X(02)
                                                VALUE SPACES.
           05 LD-CODPROD                PIC 99.999.
           05 FILLER                    PIC X(03)
                                                VALUE SPACES.
           05 LD-DESCRICAO              PIC X(25).
           05 FILLER                    PIC X(05)
                                                VALUE SPACES.
           05 LD-QTDEST                 PIC 999.
           05 FILLER                    PIC X(07)
                                                VALUE SPACES.
           05 LD-QTDMIN                 PIC 999.
           05 FILLER                    PIC X(07)
                                                VALUE SPACES.
           05 LD-QTDREP                 PIC 999.
           05 FILLER                    PIC X(06)
                                                VALUE SPACES.
           05 LD-PRECO                  PIC Z.ZZ9,99.
           05 FILLER                    PIC X(02)
                                                VALUE SPACES.
       01  WS-RODAPE1                   PIC X(80)
                                                VALUE ALL "-".
       01  WS-RODAPE2.
           05 FILLER                    PIC X(40)
                                                VALUE
                                     " INDUSTRIAS SEU NOME LTDA. ".
           05 FILLER                    PIC X(40)
                                                VALUE
                                 " SERVIMOS BEM PARA SERVIR SEMPRE ".
       01  WS-REG-ESTOQUE.
           05 WS-CODPROD-E                 PIC 9(05).
           05 WS-DESCRICAO-E               PIC X(25).
           05 WS-QTDEST-E                  PIC 9(03).
           05 WS-QTDMIN-E                  PIC 9(03).
           05 WS-QTDMAX-E                  PIC 9(03).
           05 WS-PRECO-E                   PIC 9(04)V99.
       01  WS-DATA-SYS.
           05 WS-ANO                       PIC 99.
           05 WS-MES                          PIC 99.
           05 WS-DIA                          PIC 99.
       01  WS-HORA-SYS.
           05 WS-HORA                       PIC 99.
           05 WS-MINUTO                     PIC 99.
           05 WS-SEGUNDO                    PIC 99.
       01  WS-DATA-FORMATADA.
           05 WS-DIA                        PIC 99.
           05 FILLER                        PIC X(01)
                                                   VALUE '/'.
           05 WS-MES                        PIC 99.
           05 FILLER                        PIC X(03)
                                                   VALUE '/20'.
           05 WS-ANO                        PIC 99.
       01  WS-HORA-FORMATADA.
           05 WS-HORA                       PIC 99.
           05 FILLER                        PIC X(01)
                                                    VALUE ':'.
           05 WS-MINUTO                     PIC 99.
           05 FILLER                        PIC X(01)
                                                    VALUE ':'.
           05 WS-SEGUNDO                    PIC 99.
       77  FS-ESTOQUE                       PIC X(02).
       77  FS-RELATO                        PIC X(02).
       77  WS-CTLIDO                        PIC 9(03).
       77  WS-CTDESP                        PIC 9(03).
       77  WS-CTIMP                         PIC 9(03).
       77  WS-CTLIN                         PIC 9(02).
       77  WS-CTPAG                         PIC 9(02).
       77  WS-PULA                          PIC 9(02).
       77  WS-MSG                           PIC X(60).
       77  WS-FS                            PIC X(02).
       77  WS-QTDREP                        PIC 9(03).
      *----------------------------------------------------------------*
       PROCEDURE DIVISION.
      *----------------------------------------------------------------*
       000-EEXXNN07.
           PERFORM 010-INICIALIZAR
           PERFORM 030-PROCESSAR
                      UNTIL FS-ESTOQUE = "10"
           PERFORM 070-TERMINO
           STOP RUN
           .
       010-INICIALIZAR.
           MOVE 0                           TO WS-CTLIDO
           MOVE 0                           TO WS-CTDESP
           MOVE 0                           TO WS-CTIMP
           MOVE 99                          TO WS-CTLIN
           MOVE 0                           TO WS-CTPAG
      *
           OPEN INPUT ESTOQUE
           IF FS-ESTOQUE NOT = "00"
              MOVE "ERRO ABERTURA ESTOQUE" TO WS-MSG
              MOVE FS-ESTOQUE TO WS-FS
              GO TO 999-ERRO
           END-IF
           OPEN OUTPUT RELATO
           IF FS-RELATO NOT = "00"
              MOVE "ERRO ABERTURA ESTOQUE" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
           PERFORM 999-FORMATA-DATA-HORA
           PERFORM 020-LER-ESTOQUE
           .
       020-LER-ESTOQUE.
           READ ESTOQUE INTO WS-REG-ESTOQUE
           IF FS-ESTOQUE = "00"
              ADD 1 TO WS-CTLIDO
           ELSE
              IF FS-ESTOQUE NOT = "10"
                 MOVE "ERRO LEITURA ESTOQUE" TO WS-MSG
                 MOVE FS-ESTOQUE TO WS-FS
                 GO TO 999-ERRO
              END-IF
           END-IF
           .
       030-PROCESSAR.
           IF WS-QTDEST-E < WS-QTDMIN-E
              IF WS-CTLIN > 49
                 PERFORM 040-IMPRIME-CABECALHO
              END-IF
              PERFORM 050-IMPRIME-DETALHE
              IF WS-CTLIN = 48
                 PERFORM 060-IMPRIME-RODAPE
              END-IF
           ELSE
              ADD 1 TO WS-CTDESP
           END-IF
           PERFORM 020-LER-ESTOQUE
           .
       040-IMPRIME-CABECALHO.
           MOVE WS-DATA-FORMATADA TO WS-DATA-CABEC1
           MOVE WS-HORA-FORMATADA TO WS-HORA-CABEC1
           ADD 1 TO WS-CTPAG
           MOVE WS-CTPAG TO WS-PAG-CABEC1
      *
           WRITE REG-RELATO FROM WS-CABEC1 AFTER PAGE
      *
           IF FS-RELATO NOT = "00"
              MOVE "ERRO IMPRESSAO WS-CABEC1" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
      *
           WRITE REG-RELATO FROM WS-CABEC2
      *
           IF FS-RELATO NOT = "00"
              MOVE "ERRO IMPRESSAO WS-CABEC2" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
      *
           WRITE REG-RELATO FROM WS-CABEC3
      *
           IF FS-RELATO NOT = "00"
              MOVE "ERRO IMPRESSAO WS-CABEC3" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
      *
           WRITE REG-RELATO FROM WS-CABEC4
      *
           IF FS-RELATO NOT = "00"
              MOVE "ERRO IMPRESSAO WS-CABEC4" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
           MOVE 4 TO WS-CTLIN
           .
       050-IMPRIME-DETALHE.
           MOVE WS-CODPROD-E TO LD-CODPROD
           MOVE WS-DESCRICAO-E TO LD-DESCRICAO
           MOVE WS-QTDEST-E TO LD-QTDEST
           MOVE WS-QTDMIN-E TO LD-QTDMIN
           COMPUTE WS-QTDREP = WS-QTDMAX-E - WS-QTDEST-E
           MOVE WS-QTDREP TO LD-QTDREP
           MOVE WS-PRECO-E TO LD-PRECO
      *
           WRITE REG-RELATO FROM WS-LINDET
      *
           IF FS-RELATO NOT = "00"
              MOVE "ERRO IMPRESSAO LINDET" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
           ADD 1 TO WS-CTIMP
           ADD 1 TO WS-CTLIN
           .
       060-IMPRIME-RODAPE.
           COMPUTE WS-PULA = 49 - WS-CTLIN
           WRITE REG-RELATO FROM WS-RODAPE1 AFTER WS-PULA LINES
           IF FS-RELATO NOT = "00"
              MOVE "ERRO IMPRESSAO WS-RODAPE1" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
           WRITE REG-RELATO FROM WS-RODAPE2
           IF FS-RELATO NOT = "00"
              MOVE "ERRO IMPRESSAO WS-RODAPE2" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
           MOVE 50 TO WS-CTLIN
           .
       070-TERMINO.
           IF WS-CTLIN < 50
              PERFORM 060-IMPRIME-RODAPE
           END-IF
           DISPLAY "TOTAL DE REGISTROS LIDOS = " WS-CTLIDO
           DISPLAY "TOTAL DE REGISTROS IMPRESSOS = " WS-CTIMP
           DISPLAY "TOTAL DE REGISTROS DESPREZADOS = " WS-CTDESP
      *
           CLOSE ESTOQUE
      *
           IF FS-ESTOQUE NOT = "00"
              MOVE "ERRO FECHAMENTO ESTOQUE" TO WS-MSG
              MOVE FS-ESTOQUE TO WS-FS
              GO TO 999-ERRO
           END-IF
      *
           CLOSE RELATO
      *
           IF FS-RELATO NOT = "00"
              MOVE "ERRO FECHAMENTO RELATO" TO WS-MSG
              MOVE FS-RELATO TO WS-FS
              GO TO 999-ERRO
           END-IF
           DISPLAY "*------------------------------------------*"
           DISPLAY " DEU CERTO!!! )/_ "
           DISPLAY " _.--..---'-,--O_ "
           DISPLAY " \|..' ._O__)_ "
           DISPLAY " ,-. _./ _ \..--( / "
           DISPLAY " `\.-''__.-' \ ( \_ "
           DISPLAY " `''' `\__ /\ "
           DISPLAY " /) "
           DISPLAY "*------------------------------------------*"
           DISPLAY " TERMINO NORMAL DO EEXXNN07"
           DISPLAY "*------------------------------------------*"
           .
       999-ERRO.
           DISPLAY "*------------------------------------------*"
           DISPLAY " MENSAGEM.....: " WS-MSG
           DISPLAY " FILE STATUS..: " WS-FS
           DISPLAY "*------------------------------------------*"
           DISPLAY " ._`-\ )\,`-.-. "
           DISPLAY " \'\` \)\ \)\ \|.) "
           DISPLAY " \`) |\) )\ .)\ )\| "
           DISPLAY " \ \)\ |)\ ` \ .')/| "
           DISPLAY " ``-.\ \ )\ ` . ., '( "
           DISPLAY " \\ -. `)\``- ._ .)` |\(,_ "
           DISPLAY " `__ '\ `-- _\`. ` (/ "
           DISPLAY " `\,\ .\\ / "
           DISPLAY " '` ) (`-.\\ ` "
           DISPLAY " /||\ `. * _*| "
           DISPLAY " `-.( `\ "
           DISPLAY " `. \ "
           DISPLAY " `(C "
           DISPLAY " DEU ERRO!!! "
           DISPLAY "*------------------------------------------*"
           DISPLAY " TERMINO ANORMAL DO EEXXNN07 "
           DISPLAY "*------------------------------------------*"
           STOP RUN
           .
       999-FORMATA-DATA-HORA.
           ACCEPT WS-DATA-SYS FROM DATE
           ACCEPT WS-HORA-SYS FROM TIME
           MOVE CORRESPONDING WS-DATA-SYS TO WS-DATA-FORMATADA
           MOVE CORRESPONDING WS-HORA-SYS TO WS-HORA-FORMATADA
           .
