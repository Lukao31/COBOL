      *                                                                 00060004
      *--------------------------------------------------------------*  00010004
       IDENTIFICATION                      DIVISION.                    00020004
      *--------------------------------------------------------------*  00030004
      *                                                                 00060004
       PROGRAM-ID.                         EX007P22.                    00040008
       AUTHOR.                             LUCAS.                       00050008
      *                                                                 00060004
      *--------------------------------------------------------------*  00070004
       ENVIRONMENT                         DIVISION.                    00080004
      *--------------------------------------------------------------*  00090004
      *                                                                 00060004
       CONFIGURATION                       SECTION.                     00100004
       SPECIAL-NAMES.                                                   00110004
           DECIMAL-POINT IS COMMA.                                      00120004
      *                                                                 00060004
       INPUT-OUTPUT                        SECTION.                     00110004
      *                                                                 00060004
       FILE-CONTROL.
           SELECT CADPRDD1 ASSIGN TO UT-S-CADPRDD1
           FILE STATUS IS FS-CADPRDD1
           .
      *
      *--------------------------------------------------------------*  00130004
       DATA                                DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
       FILE                                SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
       FD  CADPRDD1
           RECORDING MODE IS F
           .
      *
           COPY CADPRDD1 REPLACING ==::== BY ====.
      *--------------------------------------------------------------*  00210008
       WORKING-STORAGE                     SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
      * BOOK DA VARIAVEL ESPELHO                                     *
      *--------------------------------------------------------------*  00210008
           COPY CADPRDD1 REPLACING ==::== BY ==WS-==.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS PARA CALCULO DO TEMPO                     *
      *--------------------------------------------------------------*
           COPY VARDATA.
           COPY VARPROC.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS HOSPEDEIRAS                               *
      *--------------------------------------------------------------*
           EXEC SQL
              INCLUDE BK002TP
           END-EXEC.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS PARA O SQL                                *
      *--------------------------------------------------------------*
           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.
      *
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(04) COMP.
           05 WS-CTINS                     PIC 9(04) COMP.
           05 WS-CTDUP                     PIC 9(04) COMP.
           05 WS-CTINV                     PIC 9(04) COMP.
      *
       01  WS-CONTADORES-FORMATADOS.
           05 WS-CTLIDO-F                  PIC ZZZ9.
           05 WS-CTINS-F                   PIC ZZZ9.
           05 WS-CTDUP-F                   PIC ZZZ9.
           05 WS-CTINV-F                   PIC ZZZ9.
      *
       01  FS-CADPRDD1                     PIC X(02).
           88 SUCESSO-D1                           VALUE "00".
           88 FIM-ARQ-D1                           VALUE "10".
      *
       77  WS-SQLCODE                      PIC +9(9).
       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).
      *
      *--------------------------------------------------------------*  00210008
       PROCEDURE                           DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
      *0000-MAIN.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL FIM-ARQ-D1
           PERFORM 3000-TERMINO
           GOBACK
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
      *
           MOVE 0                          TO WS-CTLIDO
           MOVE 0                          TO WS-CTINS
           MOVE 0                          TO WS-CTDUP
           MOVE 0                          TO WS-CTINV
      *
           OPEN INPUT CADPRDD1
           IF NOT SUCESSO-D1
              MOVE "ERRO DE ABERTURA CADPRDD1" TO WS-MSG
              MOVE FS-CADPRDD1                 TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           PERFORM 1100-LER-CADPRDD1
           .
      *
       1100-LER-CADPRDD1.
           READ CADPRDD1 INTO WS-REG-CADPRDD1
           IF SUCESSO-D1
              ADD 1 TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-D1
                    MOVE "ERRO LEITURA CADPRDD1" TO WS-MSG
                    MOVE FS-CADPRDD1             TO WS-FS
                    PERFORM 9000-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
      *----------------------------------------------------------------*
      * MOVER DADOS DA VARIAVEL ESPELHO PARA A HOSPEDEIRA              *
      *----------------------------------------------------------------*
           MOVE WS-CODPROD-D1          TO CODPROD
           MOVE +20                    TO DESCPROD-LEN
           MOVE WS-DESCPROD-D1         TO DESCPROD-TEXT
           MOVE WS-UNIDPROD-D1         TO UNIDPROD
           MOVE WS-LOCALPROD-D1        TO LOCALPROD
           MOVE WS-QTDEST-D1           TO QTDEST
           MOVE WS-QTDMAX-D1           TO QTDMAX
           MOVE WS-QTDMIN-D1           TO QTDMIN
           MOVE WS-PRECOCOMPRA-D1      TO PRECOCOMPRA
           MOVE WS-PRECOVENDA-D1       TO PRECOVENDA
           MOVE WS-PERCOMIS-D1         TO PERCOMIS
      *
      *----------------------------------------------------------------*
      * INSERIR OS DADOS DA HOSPEDEIRA NA TABELA (SQL FIELDS)          *
      *----------------------------------------------------------------*
           EXEC SQL
              INSERT INTO TBPRODUTO
              (   CODPROD
                , DESCPROD
                , UNIDPROD
                , LOCALPROD
                , QTDEST
                , QTDMAX
                , QTDMIN
                , PRECOCOMPRA
                , PRECOVENDA
                , PERCOMIS
              )
              VALUES
              (   :CODPROD
                , :DESCPROD
                , :UNIDPROD
                , :LOCALPROD
                , :QTDEST
                , :QTDMAX
                , :QTDMIN
                , :PRECOCOMPRA
                , :PRECOVENDA
                , :PERCOMIS
              )
           END-EXEC
           .
      *
      *----------------------------------------------------------------*
      * TESTAR SQLCODE                                                 *
      *----------------------------------------------------------------*
           EVALUATE SQLCODE
              WHEN 0
                 ADD 1 TO WS-CTINS
              WHEN -803
                 ADD 1 TO WS-CTDUP
              WHEN -545
                 ADD 1 TO WS-CTINV
              WHEN OTHER
                 MOVE "ERRO INSERT TBPRODUTO" TO WS-MSG
                 MOVE SQLCODE                 TO WS-SQLCODE
                 PERFORM 9000-ERRO-DB2
           END-EVALUATE
      *
           PERFORM 1100-LER-CADPRDD1
           .
      *
       3000-TERMINO.
           CLOSE CADPRDD1
           IF NOT SUCESSO-D1
              MOVE "ERRO FECHAMENTO CADPRDD1"  TO WS-MSG
              MOVE FS-CADPRDD1                 TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
      *
           PERFORM 9000-CALCULA-TEMPO-PROC
      *
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTINS                   TO WS-CTINS-F
           MOVE WS-CTDUP                   TO WS-CTDUP-F
           MOVE WS-CTINV                   TO WS-CTINV-F
      *
           PERFORM 9000-IMPRIME-DATA
      *
      *
           DISPLAY '   '
           DISPLAY '   '
           DISPLAY '=================================================='
           DISPLAY '==  ESTATISTICA FINAL DE PROCESSAMENTO          =='
           DISPLAY '=================================================='
           DISPLAY 'QTDE. DE PRODUTOS LIDOS CADPRDD1...: ' WS-CTLIDO-F
           DISPLAY 'QTDE. PRODUTOS INSERIDOS TBPRODUTO.: ' WS-CTINS-F
           DISPLAY 'QTDE. PRODUTOS DUPLICADOS..........: ' WS-CTDUP-F
           DISPLAY 'QTDE. PRODUTOS INVALIDOS...........: ' WS-CTINV-F
           DISPLAY '=================================================='
           DISPLAY 'TEMPO TOTAL DE PROCESSSAMENTO........: '
                                           WS-TEMPO-PROCESSAMENTO
           DISPLAY '=================================================='
           DISPLAY '==  TERMINO NORMAL DO PROGRAMA EX007P22         =='
           DISPLAY '=================================================='
           .
      *
       9000-ERRO-DB2.
           DISPLAY '==================================================='
           DISPLAY 'MESSAGE...:' WS-MSG
           DISPLAY 'SQLCODE...:' WS-SQLCODE
           DISPLAY '==================================================='
           DISPLAY '==  TERMINO ANORMAL DO PROGRAMA EXECARGA         =='
           DISPLAY '==================================================='
           MOVE 16                         TO RETURN-CODE
           STOP RUN
           .
      *
      *--------------------------------------------------------------*  00210008
      * COPY BOOK DE ROTINAS                                            00210008
      *--------------------------------------------------------------*  00210008
           COPY ROTDATA.
           COPY ROTERRO.
           COPY ROTPROC.
