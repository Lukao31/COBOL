       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID                          EX007P19.
       AUTHOR                              LUCAS.
      *----------------------------------------------------------------*
       ENVIRONMENT                        DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT CADPRD ASSIGN               TO UT-S-CADPRD
           FILE STATUS FS-CADPRD
           .
           SELECT VCADPRD ASSIGN TO DA-I-VCADPRD
           ORGANIZATION IS INDEXED
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS FS-VCADPRD
           RECORD KEY IS CODPRD-V
           .
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       FILE                                SECTION.
      *----------------------------------------------------------------*
       FD  CADPRD
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 69 CHARACTERS
           DATA RECORD IS REG-CADPRD
           .
      *
       01  REG-CADPRD.
           05 CODPRD                       PIC X(04).
           05 DESCPROD                     PIC X(20).
           05 UNIDPROD                     PIC X(05).
           05 LOCALPROD                    PIC X(05).
           05 QTDEST                       PIC 9(05).
           05 QTDMAX                       PIC 9(05).
           05 QTDMIN                       PIC 9(05).
           05 PRECOCOMPRA                  PIC 9(06)V99.
           05 PRECOVENDA                   PIC 9(06)V99.
           05 PERCOMIS                     PIC 9(02)V99.
      *
       FD  VCADPRD
           RECORD CONTAINS 69 CHARACTERS
           DATA RECORD IS REG-VCADPRD
           .
      *
       01  REG-VCADPRD.
           05 CODPRD-V                     PIC X(04).
           05 DESCPROD-V                   PIC X(20).
           05 UNIDPROD-V                   PIC 9(05).
           05 LOCALPROD-V                  PIC 9(05).
           05 QTDEST-V                     PIC 9(05).
           05 QTDMAX-V                     PIC 9(05).
           05 QTDMIN-V                     PIC 9(05).
           05 PRECOCOMPRA-V                PIC 9(06)V99.
           05 PRECOVENDA-V                 PIC 9(06)V99.
           05 PERCOMIS-V                   PIC 9(02)V99.
      *----------------------------------------------------------*
       WORKING-STORAGE                    SECTION.
      *----------------------------------------------------------*
       01  WS-REG-CADPRD.
           05 WS-CODPRD                   PIC X(04).
           05 WS-DESCPROD                 PIC X(20).
           05 WS-UNIDPROD                 PIC X(05).
           05 WS-LOCALPROD                PIC X(05).
           05 WS-QTDEST                   PIC 9(05).
           05 WS-QTDMAX                   PIC 9(05).
           05 WS-QTDMIN                   PIC 9(05).
           05 WS-PRECOCOMPRA              PIC 9(06)V99.
           05 WS-PRECOVENDA               PIC 9(06)V99.
           05 WS-PERCOMIS                 PIC 9(02)V99.
      *
       01  WS-REG-VCADPRD.
           05 WS-CODPRD-V                 PIC X(04).
           05 WS-DESCPROD-V               PIC X(20).
           05 WS-UNIDPROD-V               PIC X(05).
           05 WS-LOCALPROD-V              PIC X(05).
           05 WS-QTDEST-V                 PIC 9(05).
           05 WS-QTDMAX-V                 PIC 9(05).
           05 WS-QTDMIN-V                 PIC 9(05).
           05 WS-PRECOCOMPRA-V            PIC 9(06)V99.
           05 WS-PRECOVENDA-V             PIC 9(06)V99.
           05 WS-PERCOMIS-V               PIC 9(02)V99.
      *
       01  WS-DATE-SYS.
           05 WS-ANO-SYS                 PIC 99.
           05 WS-MES-SYS                 PIC 99.
           05 WS-DIA-SYS                 PIC 99.
      *
       01  WS-TIME-SYS.
           05 WS-HORA-SYS                PIC 99.
           05 WS-MIN-SYS                 PIC 99.
           05 WS-SEG-SYS                 PIC 99.
           05 FILLER                     PIC XX.
      *

       01  WS-DATE-FORMATADA.
           05 WS-DIA-FORMATADA           PIC 99.
           05 FILLER                     PIC X(01)
                                                 VALUE "/".
           05 WS-MES-FORMATADA           PIC 99.
           05 FILLER                     PIC X(03)
                                                 VALUE "/20".
           05 WS-ANO-FORMATADA           PIC 99.
      *
       01  WS-TIME-FORMATADA.
           05 WS-HORA-FORMATADA          PIC 99.
           05 FILLER                     PIC X(01)
                                                 VALUE ":".
           05 WS-MIN-FORMATADA           PIC 99.
           05 FILLER                     PIC X(01)
                                                 VALUE ":".
           05 WS-SEG-FORMATADA           PIC 99.

      *----------------------------------------------------------------*
       77  FS-CADPRD                     PIC X(02).
       77  FS-VCADPRD                    PIC X(02).
       77  WS-CTLIDO                     PIC 9(03).
       77  WS-CTGRAV                     PIC 9(03).
       77  WS-CTDUP                      PIC 9(03).
       77  WS-CTFORASEQ                  PIC 9(03).
       77  WS-MSG                        PIC X(60).
       77  WS-FS                         PIC X(02).
      *----------------------------------------------------------------*
       PROCEDURE DIVISION.
      *----------------------------------------------------------------*
       0000-EX007P19.
           PERFORM 9999-IMPRIME-DATA-HORA
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                      UNTIL FS-CADPRD = "10"
           PERFORM 3000-TERMINO
           PERFORM 9999-IMPRIME-DATA-HORA
           STOP RUN
           .
       1000-INICIALIZAR.
           MOVE 0                           TO WS-CTLIDO
                                               WS-CTGRAV
                                               WS-CTDUP
                                               WS-CTFORASEQ
      *
           OPEN INPUT CADPRD
      *
           IF FS-CADPRD NOT = "00"
              MOVE "ERRO DE ABERTURA CADPRD" TO WS-MSG
              MOVE FS-CADPRD                 TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *
           OPEN OUTPUT VCADPRD
      *
           IF FS-VCADPRD NOT = "00"
              MOVE "ERRO DE ABERTURA VCADPRD" TO WS-MSG
              MOVE FS-VCADPRD                 TO WS-FS
              GO TO 9000-ERRO
           END-IF
           PERFORM 1500-LERCADPRD
           .
      *
       1500-LERCADPRD.
           READ CADPRD INTO WS-REG-CADPRD
           IF FS-CADPRD = "00"
              ADD 1                         TO WS-CTLIDO
           ELSE
              IF FS-CADPRD NOT = "10"
                 MOVE "ERRO DE LEITURA CADPRD" TO WS-MSG
                 MOVE FS-CADPRD                TO WS-FS
                 PERFORM 9000-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           MOVE WS-REG-CADPRD           TO WS-REG-VCADPRD
      *
           WRITE REG-VCADPRD            FROM WS-REG-VCADPRD
      *
           EVALUATE FS-VCADPRD
              WHEN "00"
                   ADD 1 TO WS-CTGRAV
              WHEN "21"
                   ADD 1 TO WS-CTFORASEQ
              WHEN "22"
                   ADD 1 TO WS-CTDUP
              WHEN OTHER
                   MOVE "ERRO DE GRAVACAO PRODUTO"
                                              TO WS-MSG
                   MOVE FS-VCADPRD            TO WS-FS
                   PERFORM 9000-ERRO
           END-EVALUATE
           PERFORM 1500-LERCADPRD
           .
      *
       3000-TERMINO.
           DISPLAY "==================================================="
           DISPLAY "TOTAL DE REGISTROS LIDOS.....:"  WS-CTLIDO
           DISPLAY "TOTAL DE REGISTROS GRAVADOS..:"  WS-CTGRAV
           DISPLAY "TOTAL DE REGISTROS DUPLICADOS:"  WS-CTDUP
           DISPLAY "TOTAL DE REGISTROS FORA SEQ..:"  WS-CTFORASEQ
           DISPLAY "==================================================="
      *
           CLOSE CADPRD
      *
           IF FS-CADPRD NOT = "00"
              MOVE "ERRO DE FECHAMENTO CADPRD" TO WS-MSG
              MOVE FS-CADPRD                   TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           CLOSE VCADPRD
      *
           IF FS-VCADPRD NOT = "00"
              MOVE "ERRO DE FECHAMENTO CADPRD" TO WS-MSG
              MOVE FS-VCADPRD                  TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           DISPLAY "*------------------------------------------*"
           DISPLAY " DEU CERTO!!!"
           DISPLAY "*------------------------------------------*"
           DISPLAY " TERMINO NORMAL DO EX007P18"
           DISPLAY "*------------------------------------------*"
           .
      *
       9000-ERRO.
           DISPLAY "*------------------------------------------*"
           DISPLAY " MENSAGEM.....: " WS-MSG
           DISPLAY " FILE STATUS..: " WS-FS
           DISPLAY "*------------------------------------------*"
           DISPLAY " TERMINO ANORMAL DO EX007P19 "
           DISPLAY "*------------------------------------------*"
           STOP RUN
           .
      *
       9999-IMPRIME-DATA-HORA.
           ACCEPT WS-DATE-SYS           FROM DATE
      *
           MOVE WS-ANO-SYS              TO WS-ANO-FORMATADA
           MOVE WS-MES-SYS              TO WS-MES-FORMATADA
           MOVE WS-DIA-SYS              TO WS-DIA-FORMATADA
      *
           ACCEPT WS-TIME-SYS           FROM TIME
      *
           MOVE WS-HORA-SYS             TO WS-HORA-FORMATADA
           MOVE WS-MIN-SYS              TO WS-MIN-FORMATADA
           MOVE WS-SEG-SYS              TO WS-SEG-FORMATADA
      *
           DISPLAY "*-------------------------------------------------*"
           DISPLAY " DATA : "
                   WS-DATE-FORMATADA
                   " HORA : "
                   WS-TIME-FORMATADA
           DISPLAY "*-------------------------------------------------*"
           .

