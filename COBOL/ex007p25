      *                                                                 00060004
      *--------------------------------------------------------------*  00010004
       IDENTIFICATION                      DIVISION.                    00020004
      *--------------------------------------------------------------*  00030004
      *                                                                 00060004
       PROGRAM-ID.                         EX007P25.                    00040008
       AUTHOR.                             LUCAS.                       00050008
      *                                                                 00060004
      *--------------------------------------------------------------*  00070004
       ENVIRONMENT                         DIVISION.                    00080004
      *--------------------------------------------------------------*  00090004
      *                                                                 00060004
       CONFIGURATION                       SECTION.                     00100004
       SPECIAL-NAMES.                                                   00110004
           DECIMAL-POINT IS COMMA.                                      00120004
      *                                                                 00060004
       INPUT-OUTPUT                        SECTION.                     00110004
      *                                                                 00060004
       FILE-CONTROL.
           SELECT MOVPRDD2 ASSIGN TO UT-S-MOVPRDD2
           FILE STATUS IS FS-MOVPRDD2
           .
      *
      *--------------------------------------------------------------*  00130004
       DATA                                DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
       FILE                                SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
       FD  MOVPRDD2
           RECORDING MODE IS F
           .
      *
           COPY MOVPRDD2 REPLACING ==::== BY ====.
      *--------------------------------------------------------------*  00210008
       WORKING-STORAGE                     SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
      * BOOK DA VARIAVEL ESPELHO                                     *
      *--------------------------------------------------------------*  00210008
           COPY MOVPRDD2 REPLACING ==::== BY ==WS-==.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS PARA CALCULO DO TEMPO                     *
      *--------------------------------------------------------------*
           COPY VARDATA.
           COPY VARPROC.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS HOSPEDEIRAS                               *
      *--------------------------------------------------------------*
           EXEC SQL
              INCLUDE BK002TP
           END-EXEC.

      *
       01  INDICADORES.
           05 IUNIDPROD                   PIC S9(04) COMP.
           05 ILOCALPROD                  PIC S9(04) COMP.
           05 IQTDEST                     PIC S9(04) COMP.
           05 IQTDMAX                     PIC S9(04) COMP.
           05 IQTDMIN                     PIC S9(04) COMP.
           05 IPRECOCOMPRA                PIC S9(04) COMP.
           05 IPRECOVENDA                 PIC S9(04) COMP.
           05 IPERCOMIS                   PIC S9(04) COMP.
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS PARA O SQL                                *
      *--------------------------------------------------------------*
           EXEC SQL
             INCLUDE SQLCA
           END-EXEC.
      *
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(05) COMP.
           05 WS-CTINS                     PIC 9(05) COMP.
           05 WS-CTEXC                     PIC 9(05) COMP.
           05 WS-CTINV                     PIC 9(05) COMP.
           05 WS-CTALT                     PIC 9(05) COMP.
           05 WS-CTCON                     PIC 9(05) COMP.
      *
       01  WS-CONTADORES-FORMATADOS.
           05 WS-CTLIDO-F                  PIC ZZ.ZZ9.
           05 WS-CTINS-F                   PIC ZZ.ZZ9.
           05 WS-CTEXC-F                   PIC ZZ.ZZ9.
           05 WS-CTINV-F                   PIC ZZ.ZZ9.
           05 WS-CTALT-F                   PIC ZZ.ZZ9.
           05 WS-CTCON-F                   PIC ZZ.ZZ9.
      *
       01  FS-MOVPRDD2                     PIC X(02).
           88 SUCESSO-MOVPRDD2                     VALUE "00".
           88 FIM-ARQ-MOVPRDD2                     VALUE "10".
      *
      *77  WS-SALDOCLI-F                   PIC ZZ.ZZ9,99.
       77  WS-SQLCODE                      PIC +9(9).
       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).
      *
      *--------------------------------------------------------------*  00210008
       PROCEDURE                           DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
      *0000-MAIN.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL FIM-ARQ-MOVPRDD2
           PERFORM 3000-TERMINO
           STOP RUN
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
      *
           INITIALIZE WS-CONTADORES-COMP
      *
           OPEN INPUT MOVPRDD2
           IF NOT SUCESSO-MOVPRDD2
              MOVE "ERRO DE ABERTURA MOVPRDD2" TO WS-MSG
              MOVE FS-MOVPRDD2                 TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           PERFORM 1100-LER-MOVPRDD2
           .
      *
       1100-LER-MOVPRDD2.
           READ MOVPRDD2 INTO WS-REG-MOVPRDD2
           IF SUCESSO-MOVPRDD2
              ADD 1 TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-MOVPRDD2
                    MOVE "ERRO LEITURA MOVPRDD2" TO WS-MSG
                    MOVE FS-MOVPRDD2             TO WS-FS
                    PERFORM 9000-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           EVALUATE WS-TIPOMOVTO-D2
              WHEN "I" PERFORM 2100-INCLUSAO
              WHEN "E" PERFORM 2200-EXCLUSAO
              WHEN "A" PERFORM 2300-ALTERACAO
              WHEN "C" PERFORM 2400-CONSULTAR
              WHEN OTHER
                 ADD 1 TO WS-CTINV
           END-EVALUATE
      *
           IF WS-TIPOMOVTO-D2 = 'I' OR 'E' OR 'A' OR 'C'
              IF SQLCODE = 0
                 EXEC SQL COMMIT END-EXEC
              ELSE
                 EXEC SQL ROLLBACK END-EXEC
              END-IF
           END-IF
      *    PERFORM 1100-LER-MOVPRDD2
           .
      *
       2100-INCLUSAO.
      *----------------------------------------------------------------*
      * MOVER DADOS DA VARIAVEL ESPELHO PARA A HOSPEDEIRA              *
      *----------------------------------------------------------------*
           MOVE WS-CODPROD-D2          TO CODPROD
           MOVE +20                    TO DESCPROD-LEN
           MOVE WS-DESCPROD-D2         TO DESCPROD-TEXT
           MOVE WS-UNIDPROD-D2         TO UNIDPROD
           MOVE WS-LOCALPROD-D2        TO LOCALPROD
           MOVE WS-QTDEST-D2           TO QTDEST
           MOVE WS-QTDMAX-D2           TO QTDMAX
           MOVE WS-QTDMIN-D2           TO QTDMIN
           MOVE WS-PRECOCOMPRA-D2      TO PRECOCOMPRA
           MOVE WS-PRECOVENDA-D2       TO PRECOVENDA
           MOVE WS-PERCOMIS-D2         TO PERCOMIS
      *
      *----------------------------------------------------------------*
      * INSERIR OS DADOS DA HOSPEDEIRA NA TABELA (SQL FIELDS)          *
      *----------------------------------------------------------------*
           EXEC SQL
              INSERT INTO TBPRODUTO
              (   CODPROD
                , DESCPROD
                , UNIDPROD
                , LOCALPROD
                , QTDEST
                , QTDMAX
                , QTDMIN
                , PRECOCOMPRA
                , PRECOVENDA
                , PERCOMIS
              )
              VALUES
              (   :CODPROD
                , :DESCPROD
                , :UNIDPROD
                , :LOCALPROD
                , :QTDEST
                , :QTDMAX
                , :QTDMIN
                , :PRECOCOMPRA
                , :PRECOVENDA
                , :PERCOMIS
              )
           END-EXEC.
      *
      *----------------------------------------------------------------*
      * TESTAR SQLCODE                                                 *
      *----------------------------------------------------------------*
           EVALUATE SQLCODE
              WHEN 0
                 ADD 1 TO WS-CTINS
              WHEN -803
                 ADD 1 TO WS-CTINV
              WHEN -545
                 ADD 1 TO WS-CTINV
              WHEN OTHER
                 MOVE "ERRO INSERT NA TBPRODUTO" TO WS-MSG
                 MOVE SQLCODE                    TO WS-SQLCODE
                 PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       2200-EXCLUSAO.
           MOVE WS-CODPROD-D2                TO CODPROD
      *
           EXEC SQL
              DELETE FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0    ADD 1    TO WS-CTEXC
              WHEN +100 ADD 1    TO WS-CTINV
              WHEN OTHER
              MOVE "ERRO DELETE NA TABELA TBPRODUTO"
                                         TO WS-MSG
              MOVE SQLCODE               TO WS-SQLCODE
              PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       2300-ALTERACAO.
           MOVE WS-CODPROD-D2            TO CODPROD
      *
           EXEC SQL
              SELECT CODPROD
                   , DESCPROD
                   , UNIDPROD
                   , LOCALPROD
                   , QTDEST
                   , QTDMAX
                   , QTDMIN
                   , PRECOCOMPRA
                   , PRECOVENDA
                   , PERCOMIS
              INTO   :CODPROD
                   , :DESCPROD
                   , :UNIDPROD    :IUNIDPROD
                   , :LOCALPROD   :ILOCALPROD
                   , :QTDEST      :IQTDEST
                   , :QTDMAX      :IQTDMAX
                   , :QTDMIN      :IQTDMIN
                   , :PRECOCOMPRA :IPRECOCOMPRA
                   , :PRECOVENDA  :IPRECOVENDA
                   , :PERCOMIS    :IPERCOMIS
              FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC
      *
           EVALUATE SQLCODE
             WHEN 0
              PERFORM 9000-TRATA-INDICATOR
              PERFORM 2310-UPDATE
             WHEN +100
              ADD 1 TO WS-CTINV
             WHEN OTHER
                MOVE "ERRO SELECT DO UPDATE NA TABELA TBPRODUTO"
                                               TO WS-MSG
                MOVE SQLCODE                   TO WS-SQLCODE
                PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       2310-UPDATE.
      * TESTE DA VARIAVEL ESPELHO
      *    IF WS-CODPROD-D2  NOT = SPACES
      *       MOVE WS-CODPROD-D2               TO CODPROD
      *    END-IF
      *
           IF WS-DESCPROD-D2 NOT = SPACES
              MOVE +20                         TO DESCPROD-LEN
              MOVE WS-DESCPROD-D2              TO DESCPROD-TEXT
           END-IF
      *
           IF WS-UNIDPROD-D2 NOT = SPACES
              MOVE WS-UNIDPROD-D2              TO UNIDPROD
           END-IF
      *
           IF WS-LOCALPROD-D2 NOT = SPACES
              MOVE WS-LOCALPROD-D2             TO LOCALPROD
           END-IF
      *
           IF WS-QTDEST-D2 IS NUMERIC
              MOVE WS-QTDEST-D2                TO QTDEST
           END-IF
      *
           IF WS-QTDMAX-D2 IS NUMERIC
              MOVE WS-QTDMAX-D2                TO QTDMAX
           END-IF
      *
           IF WS-QTDMIN-D2 IS NUMERIC
              MOVE WS-QTDMIN-D2                TO QTDMIN
           END-IF
      *
           IF WS-PRECOCOMPRA-D2 IS NUMERIC
              MOVE WS-PRECOCOMPRA-D2           TO PRECOCOMPRA
           END-IF
      *
           IF WS-PRECOVENDA-D2 IS NUMERIC
              MOVE WS-PRECOVENDA-D2            TO PRECOVENDA
           END-IF
      *
           IF WS-PERCOMIS-D2 IS NUMERIC
              MOVE WS-PERCOMIS-D2              TO PERCOMIS
           END-IF
      *
           EXEC SQL
              UPDATE TBPRODUTO
              SET    DESCPROD    = :DESCPROD
                   , UNIDPROD    = :UNIDPROD
                   , LOCALPROD   = :LOCALPROD
                   , QTDEST      = :QTDEST
                   , QTDMAX      = :QTDMAX
                   , QTDMIN      = :QTDMIN
                   , PRECOCOMPRA = :PRECOCOMPRA
                   , PRECOVENDA  = :PRECOVENDA
                   , PERCOMIS    = :PERCOMIS
              WHERE  CODPROD     = :CODPROD
           END-EXEC

      *
           EVALUATE SQLCODE
              WHEN 0    ADD 1 TO WS-CTALT
              WHEN +100 ADD 1 TO WS-CTINV
              WHEN -545 ADD 1 TO WS-CTINV
              WHEN OTHER
                   MOVE "ERRO UPDATE NA TABELA TBPRODUTO"
                                           TO WS-MSG
                   MOVE SQLCODE            TO WS-SQLCODE
                   PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
       2400-CONSULTAR.
           MOVE WS-CODPROD-D2             TO CODPROD
      *
           EXEC SQL
              SELECT CODPROD
                   , DESCPROD
                   , UNIDPROD
                   , LOCALPROD
                   , QTDEST
                   , QTDMAX
                   , QTDMIN
                   , PRECOCOMPRA
                   , PRECOVENDA
                   , PERCOMIS
              INTO   :CODPROD
                   , :DESCPROD
                   , :UNIDPROD    :IUNIDPROD
                   , :LOCALPROD   :ILOCALPROD
                   , :QTDEST      :IQTDEST
                   , :QTDMAX      :IQTDMAX
                   , :QTDMIN      :IQTDMIN
                   , :PRECOCOMPRA :IPRECOCOMPRA
                   , :PRECOVENDA  :IPRECOVENDA
                   , :PERCOMIS    :IPERCOMIS
              FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC
           .
      *
           EVALUATE SQLCODE
              WHEN 0    ADD 1 TO WS-CTCON
                        PERFORM 9000-TRATA-INDICATOR
                        PERFORM 9000-EXIBE-CONSULTA
              WHEN +100 ADD 1 TO WS-CTINV
              WHEN OTHER
                 MOVE "ERRO CONSULTA NA TABELA TBPRODUTO"
                                              TO WS-MSG
                 MOVE SQLCODE                 TO WS-SQLCODE
                 PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       3000-TERMINO.
           CLOSE MOVPRDD2
           IF NOT SUCESSO-MOVPRDD2
              MOVE "ERRO FECHAMENTO MOVPRDD2"  TO WS-MSG
              MOVE FS-MOVPRDD2                 TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
      *
           PERFORM 9000-CALCULA-TEMPO-PROC
      *
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTINS                   TO WS-CTINS-F
           MOVE WS-CTEXC                   TO WS-CTEXC-F
           MOVE WS-CTINV                   TO WS-CTINV-F
           MOVE WS-CTCON                   TO WS-CTCON-F
           MOVE WS-CTALT                   TO WS-CTALT-F
      *
           PERFORM 9000-IMPRIME-DATA
      *
      *
           DISPLAY '   '
           DISPLAY '   '
           DISPLAY '=================================================='
           DISPLAY '==  ESTATISTICA FINAL DE PROCESSAMENTO          =='
           DISPLAY '=================================================='
           DISPLAY 'QTDE. DE MOVIMENTOS LIDOS MOVPRDD2. : ' WS-CTLIDO-F
           DISPLAY 'QTDE. CLIENTES INSERIDOS TBPRODUTO. : ' WS-CTINS-F
           DISPLAY 'QTDE. CLIENTES EXCLUIDOS............: ' WS-CTEXC-F
           DISPLAY 'QTDE. CLIENTES ALTERADOS TBPRODUTO. : ' WS-CTALT-F
           DISPLAY 'QTDE. CLIENTES CONSULTADOS TBPRODUTO: ' WS-CTCON-F
           DISPLAY 'QTDE. MOVIMENTOS INVALIDOS......... : ' WS-CTINV-F
           DISPLAY '=================================================='
           DISPLAY 'TEMPO TOTAL DE PROCESSSAMENTO........: '
                                           WS-TEMPO-PROCESSAMENTO
           DISPLAY '=================================================='
           DISPLAY '==  TERMINO NORMAL DO PROGRAMA EX007P25         =='
           DISPLAY '=================================================='
           .
      *
       9000-TRATA-INDICATOR.
      * TESTE DOS INDICATORS
           IF IUNIDPROD LESS ZERO
              MOVE SPACES              TO UNIDPROD
           END-IF
      *
           IF ILOCALPROD LESS ZERO
              MOVE SPACES              TO LOCALPROD
           END-IF
      *
           IF IQTDEST LESS ZERO
              MOVE ZEROS               TO QTDEST
           END-IF
      *
           IF IQTDMAX LESS ZERO
              MOVE ZEROS               TO QTDMAX
           END-IF
      *
           IF IQTDMIN LESS ZERO
              MOVE ZEROS               TO QTDMIN
           END-IF
      *
           IF IPRECOCOMPRA LESS ZERO
              MOVE ZEROS               TO PRECOCOMPRA
           END-IF
      *
           IF IPRECOVENDA LESS ZERO
              MOVE ZEROS               TO PRECOVENDA
           END-IF
      *
           IF IPERCOMIS LESS ZERO
              MOVE ZEROS               TO PERCOMIS
           END-IF
           .
      *
       9000-EXIBE-CONSULTA.
           DISPLAY '==================================================='
           DISPLAY '==              CONSULTA DE CLIENTE              =='
           DISPLAY '==================================================='
           DISPLAY 'CODIGO.....:' CODPROD
           DISPLAY 'DESCRICAO..:' DESCPROD-TEXT
           DISPLAY 'UNIDADE....:' UNIDPROD
           DISPLAY 'LOCALIZACAO:' LOCALPROD
           DISPLAY 'QTD EM EST.:' QTDEST
           DISPLAY 'QTD MAX EST:' QTDMAX
           DISPLAY 'QTD MIN EST:' QTDMIN
           DISPLAY 'COMPRA.....:' PRECOCOMPRA
           DISPLAY 'VENDA......:' PRECOVENDA
           DISPLAY 'PERCENTUAL.:' PERCOMIS
           DISPLAY '==================================================='
           .
      *
       9000-ERRO-DB2.
           DISPLAY '==================================================='
           DISPLAY 'MESSAGE...:' WS-MSG
           DISPLAY 'SQLCODE...:' WS-SQLCODE
           DISPLAY '==================================================='
           DISPLAY '==  TERMINO ANORMAL DO PROGRAMA EX007P25         =='
           DISPLAY '==================================================='
           MOVE 16                         TO RETURN-CODE
           STOP RUN
           .
      *
      *--------------------------------------------------------------*  00210008
      * COPY BOOK DE ROTINAS                                            00210008
      *--------------------------------------------------------------*  00210008
           COPY ROTDATA.
           COPY ROTERRO.
           COPY ROTPROC.
