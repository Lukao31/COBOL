      *--------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *--------------------------------------------------------------*
       PROGRAM-ID.                         FS07PGMM.
       AUTHOR.                             LUCAS.

      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *--------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.
       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
      *
       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).
      *
           COPY FS07MNU.
           COPY DFHAID.
           COPY DFHBMSCA.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24576 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*
           EXEC CICS HANDLE CONDITION
              MAPFAIL (999-ERRO-MAPA)
              ERROR(999-ERRO-GENERICO)
           END-EXEC
      *
      * SELETOR DE FASE - O MENU PRINCIPAL POSSUI 2 FASES
      *    FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *    FASE 2 - TRATA O CAMPO T1OPC
      *---------------------------------------------------------------*A
           MOVE DFHCOMMAREA                 TO WS-DFHCOMMAREA
      *
           IF EIBCALEN EQUAL 0
              MOVE '1'                      TO WS-FASE
           END-IF
      *
           EVALUATE WS-FASE
           WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN OTHER
                 MOVE +80                   TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                            TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .
      *
       100-FASE1.
      * LIMPAR AS VARIAVEIS DO MAPA
           MOVE LOW-VALUES                 TO MAPAMNUO
      * POSICIONAMENTO LOGICO - DIRETO NO CAMPO DESEJADO
           MOVE -1                         TO T1OPCL
      * MOVER A MENSAGEM PARA O CAMPO APROPRIADO
           MOVE 'SELECIONE A OPCAO E PRESSIONE ENTER'
                                           TO T1MSGO
      *
      * DESPROTEGER O CAMPO OPCAO PARA O USUARIO PODER DIGITAR
           MOVE DFHUNIMD                   TO T1OPCA
      * MANDAR A TELA PARA O TERMINAL
           PERFORM 999-MANDA-TELA
      * ENCERRA A TRANSACAO, CHAMANDO A PROXIMA FASE
           PERFORM 999-CHAMA-FASE2
           .
      *
       200-FASE2.
           EXEC CICS HANDLE AID
                ENTER  (210-ENTER)
                PF3    (220-PF3)
                PF5    (230-PF5)
                CLEAR  (230-PF5)
                ANYKEY (240-ANYKEY)
           END-EXEC
      *
           EXEC CICS RECEIVE
                MAP    ('MAPAMNU')
                MAPSET ('FS07MNU')
                INTO   (MAPAMNUI)
           END-EXEC
           .
      *
       210-ENTER.
           IF T1OPCL EQUAL 0 OR T1OPCI EQUAL SPACES
              MOVE 'ESCOLHA UMA OPCAO VALIDA'
                                           TO T1MSGO
              PERFORM 999-TRATA-FASE2
           END-IF
      *
           EVALUATE T1OPCI
              WHEN 'A'
                  MOVE '1'                             TO WS-FASE
      *
                  EXEC CICS XCTL
                      PROGRAM('FS07PGMA')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'B'
                  MOVE '1'                             TO WS-FASE
      *
                  EXEC CICS XCTL
                      PROGRAM('FS07PGMB')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'C'
                  MOVE '1'                             TO WS-FASE

                  EXEC CICS XCTL
                      PROGRAM('FS07PGMC')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'D'
                  MOVE '1'                             TO WS-FASE

                  EXEC CICS XCTL
                      PROGRAM('FS07PGMD')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'E'
                  MOVE '1'                            TO WS-FASE
                  EXEC CICS XCTL
                      PROGRAM('FS07PGME')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'F'
                  MOVE '1'                             TO WS-FASE
                  EXEC CICS XCTL
                      PROGRAM('FS07PGMF')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'G'
                  MOVE '1'                             TO WS-FASE
                  EXEC CICS XCTL
                      PROGRAM('FS07PGMG')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'H'
                  MOVE '1'                             TO WS-FASE
                  EXEC CICS XCTL
                      PROGRAM('FS07PGMH')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'I'
                  MOVE '1'                             TO WS-FASE
                  EXEC CICS XCTL
                      PROGRAM('FS07PGMI')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
      *
              WHEN 'J'
                  MOVE '1'                             TO WS-FASE
                  EXEC CICS XCTL
                      PROGRAM('FS07PGMJ')
                      COMMAREA(WS-DFHCOMMAREA)
                      LENGTH(LENGTH OF WS-DFHCOMMAREA)
                  END-EXEC
              WHEN OTHER
                   MOVE 'OPCAO INVALIDA - TENTE NOVAMENTE'
                                                      TO T1MSGO
           END-EVALUATE
           PERFORM 999-TRATA-FASE2
           .
      *
       220-PF3.
           MOVE 'TERMINO NORMAL DA TRANSACAO F07M'
                                           TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
      *
       230-PF5.
           PERFORM 999-CHAMA-FASE1
           .
      *
       240-ANYKEY.
           MOVE 'TECLA INVALIDA'           TO T1MSGO
           PERFORM 999-TRATA-FASE2
           .
      *
       999-ENCERRA-TRANSACAO.
           EXEC CICS SEND TEXT
              FROM (WS-MSG-ERRO)
              LENGTH (+80)
              ERASE FREEKB ALARM
           END-EXEC
      *
           EXEC CICS RETURN
           END-EXEC
           .
      *
      *999-PROTECAO-FASE2.
      * USAR OS SEGUINTES MNEMONICOS DA DFHBMSCA:
      *     DFHBMPRF - (PROT, ALFA, NORM, ON) - PARA PROTEGER
      *     DFHUNIMD - (UNPROT, ALFA, BRT, ON) - PARA DESPROTEGER ALFA
      *     DFHUNINT - (UNPROT, NUM, BRT, ON) - PARA DESPROTEGER NUM
      *    MOVE DFHBMPRF                      TO T2...
      *    MOVE DFHUNIMD                      TO T2...
      *    MOVE DFHUNINT                      TO T2...
      *    .
      *
       999-MANDA-TELA.
           MOVE EIBTRMID                   TO T1TERMO
           MOVE EIBTRNID                   TO T1TRANO
           MOVE EIBTASKN                   TO T1TASKO
           MOVE WS-FASE                    TO T1FASEO
      *
           EXEC CICS ASSIGN
              USERID(T1USRO)
           END-EXEC
      *
           EXEC CICS LINK
              PROGRAM('AUXCICS1')
              COMMAREA(WS-VAR-TEMPO)
              LENGTH(+18)
           END-EXEC
      *
           MOVE WS-DATA                     TO T1DATAO
           MOVE WS-HORARIO                  TO T1HORAO
      *
           EXEC CICS SEND
              MAP    ('MAPAMNU')
              MAPSET ('FS07MNU')
              FROM   (MAPAMNUO)
              ERASE FREEKB ALARM CURSOR
           END-EXEC
           .
      *
       999-CHAMA-FASE1.
           MOVE '1'                         TO WS-FASE
      *
           EXEC CICS XCTL
              PROGRAM ('FS07PGMM')
              COMMAREA (WS-DFHCOMMAREA)
              LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *
       999-CHAMA-FASE2.
           MOVE '2'                         TO WS-FASE
      *
           EXEC CICS RETURN
              TRANSID ('F07M')
              COMMAREA (WS-DFHCOMMAREA)
              LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *
       999-TRATA-FASE2.
           MOVE -1                           TO T1OPCL
      *    PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .
      *
       999-ERRO-MAPA.
           MOVE 'ERRO MAPA FS07MNU'          TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
      *
       999-ERRO-GENERICO.
           MOVE 'ERRO GENERICO FS07PGMM'     TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
