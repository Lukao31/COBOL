      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         T08Pprl.
       AUTHOR.                             LUIS GUSTAVO.
      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
           05 WS-PRLPAG                    PIC 9(02).
           05 WS-PRLPAGD                   PIC 9(02).
       01  WS-DFHCOMMAREA-AUX.
           05 WS-FASE-COMMAREA             PIC X(01).
           05 WS-IDPROD-COMMAREA           PIC X(04).

       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).
      *
       77  WS-QUEUE-NAME                   PIC X(08)
                                                   VALUE 'TS08PPRL'.
       77  ws-trim                         pic 9(02).
       77  WS-ALL-SPACES                   PIC X(15) VALUE SPACES.
       77  ws-prod-aux                     pic x(15).
       77  ws-venda-aux                    pic x(15).
       01  LD-LINDET.
           05 FILLER                       PIC X(06)
                                                   VALUE SPACES.
           05 LD-DESCPROD                  PIC X(24).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.
           05 LD-VENDPROD                  PIC X(20).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(01)
                                                   VALUE '$'.
           05 LD-VALORPROD                 PIC Z.ZZZ.ZZ9,99.
           05 FILLER                       PIC X(07)
                                                   VALUE SPACES.
       77  WS-ITEM                         PIC S9(04) COMP.
       77  WS-RESP                         PIC S9(08) COMP.
       77  WS-RESTO                        PIC 9(01).
       77  WS-INDEX                        PIC 9(02).

       01  WS-ENDFILE-AUX                  PIC X(01).
           88 ENDFILE-YES                          VALUE 'S'.
           88 ENDFILE-NO                           VALUE 'N'.
       77  ws-filtro                        pic x(15).
       77  ws-filtro-v                      pic x(15).
       77  ws-valor                         PIC S9(5)V9(2) COMP-3.
       77  ws-valor-total                   PIC S9(5)V9(2) COMP-3.

       77  WS-DESCPROD-AUX                  PIC X(24).

           COPY T08MPRL.
           COPY DFHAID.
           COPY DFHBMSCA.
      *
           EXEC SQL
              INCLUDE DCLPROD
           END-EXEC.
      *
           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24576 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
           EXEC CICS HANDLE CONDITION
              NOTFND(999-NOTFND)
              MAPFAIL(999-MAPFAIL)
              ERROR(999-ERRO-GENERICO)
           END-EXEC
      ******************************************************************
      *         SELETOR DE FASE - O MENU PRINCIPAL POSSUI 2 FASES
      *         FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *         FASE 2 - TRATA O CAMPO T2COD
      *         FASE 3 - TRATA O CAMPO T2CONF
      ******************************************************************
           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA
      *
           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
      *       WHEN '3' PERFORM 300-FASE3
      *       WHEN OTHER
      *            MOVE +80                TO WS-LENGTH
      *            MOVE 'ERRO NO NUMERO DA FASE'
      *                                    TO MENLOGO
      *
      *            PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .
       100-FASE1.
      * LIMPAR AS VARIAVEIS DO MAPA
           MOVE LOW-VALUES                  TO MAPAPRLO
      * POSICINAMENTO LOGICO - DIRETO NO CAMPO DESEJADO
           MOVE -1                          TO PRLPRODL
      * MOVER A MENSAGEM PARA O USUARIO SELECIONAR A OPCAO
           MOVE 'DIGITE O FILTRO DESEJADO!'
                                            TO PRLMSGO
      *    MOVE PRLPRODI                   TO WS-DESCPROD-AUX

           perform 210-ENTER
      *    PERFORM 999-CARREGA-DETALHES
      * MANDAR A TELA PARA O TERMINAL
           PERFORM 999-TRATA-FASE2
      * ENCERRA A TRANSACAO AO CHAMAR PROX FASE
      *    PERFORM 999-CHAMA-FASE2
           .
       200-FASE2.
           EXEC CICS HANDLE AID
              ENTER   (210-ENTER)
              PF1     (220-PF1)
              PF7     (230-PF7)
              PF8     (230-PF8)
              PF10    (260-PF10)
              PF11    (280-PF11)
              PF12    (290-PF12)
              ANYKEY  (240-ANYKEY)
           END-EXEC
      *
           EXEC CICS RECEIVE
                MAP     ('MAPAPRL')
                MAPSET  ('T08MPRL')
                INTO    (MAPAPRLI)
           END-EXEC
           .
       210-ENTER.
           if prlprodl <= 0
              MOVE SPACES                      TO WS-FILTRO
           end-if
           if prlprodl > 0
              move 0                             to ws-trim
              move prlprodi                      to ws-prod-aux
              PERFORM VARYING WS-TRIM FROM LENGTH OF ws-prod-aux
                 BY -1 UNTIL WS-TRIM = 0 OR ws-prod-aux(WS-TRIM:1)
                 NOT = SPACES
              END-PERFORM
              IF WS-TRIM = 0
                 MOVE SPACES                      TO WS-FILTRO
              ELSE
                 MOVE '%'                        TO WS-FILTRO
                 ADD 1                           TO WS-TRIM
                 MOVE ws-prod-aux                TO WS-FILTRO(2:WS-TRIM)
                 ADD 1                           TO WS-TRIM
                 MOVE '%' TO WS-FILTRO(WS-TRIM:1)
              END-IF
           end-if
           if PRLVENl <= 0
              MOVE SPACES                      TO WS-FILTRO-v
           end-if
           if PRLVENl > 0
              move 0                             to ws-trim
              move PRLVENi                      to ws-venda-aux
              PERFORM VARYING WS-TRIM FROM LENGTH OF ws-venda-aux
                 BY -1 UNTIL WS-TRIM = 0 OR ws-VENDA-aux(WS-TRIM:1)
                 NOT = SPACES
              END-PERFORM
              IF WS-TRIM = 0
                 MOVE SPACES                      TO WS-FILTRO-v
              ELSE
                 MOVE '%'                        TO WS-FILTRO-v
                 ADD 1                           TO WS-TRIM
                 MOVE ws-venda-aux             TO WS-FILTRO-v(2:WS-TRIM)
                 ADD 1                           TO WS-TRIM
                 MOVE '%' TO WS-FILTRO-v(WS-TRIM:1)
              END-IF
           end-if
           IF PRLVALl NOT EQUAL 0
              EXEC CICS BIF DEEDIT FIELD(PRLVALi) END-EXEC
           END-IF
           IF PRLVALTl NOT EQUAL 0
              EXEC CICS BIF DEEDIT FIELD(prlvalti) END-EXEC
           END-IF
           if prlvali <= prlvalti
              if PRLVALl = 0 or PRLVALi = spaces
                 move 0                           to ws-valor
              else
                 move prlvali                     to ws-valor
              end-if
              if PRLVALTl = 0 or PRLVALTi = spaces
                 move 99999,99                  to ws-valor-total
              else
                 move prlvalti                    to ws-valor-total
              end-if
           else
              move 'valor incompativel'           to prlmsgo
              perform 999-trata-fase2
           end-if
           EXEC SQL
              DECLARE CURPROD CURSOR FOR
                  SELECT  NOME_PRODUTO,VENDIDO_POR, VALOR_UNITARIO
                  FROM PRODUTOS
                  WHERE
                      (NOME_PRODUTO like :WS-FILTRO or
                       :WS-FILTRO = :WS-ALL-SPACES) and
                      (VENDIDO_POR like :WS-FILTRO-v or
                       :WS-FILTRO-v = :WS-ALL-SPACES) and
                      (VALOR_UNITARIO BETWEEN :ws-valor and
                       :ws-valor-total)
                  ORDER BY NOME_PRODUTO
           END-EXEC
           EXEC SQL
                OPEN CURPROD
           END-EXEC
           IF SQLCODE NOT EQUAL 0
              MOVE "ERRO AO ABRIR  CURSOR" TO WS-MSG-ERRO
              PERFORM 999-ENCERRA-TRANSACAO
           END-IF
           EXEC CICS DELETEQ TS
              QUEUE(WS-QUEUE-NAME)
              RESP(WS-RESP)
           END-EXEC

           MOVE ZERO                       TO WS-ITEM
           SET ENDFILE-NO TO TRUE
           PERFORM 999-CARREGA-TS UNTIL ENDFILE-YES

           EXEC SQL
               CLOSE CURPROD
           END-EXEC
           IF SQLCODE NOT EQUAL 0
              MOVE "ERRO AO FECHAR CURSOR" TO WS-MSG-ERRO
              PERFORM 999-ENCERRA-TRANSACAO
           END-IF

           MOVE 1                          TO WS-PRLPAG

           DIVIDE WS-ITEM BY 10 GIVING WS-PRLPAGD REMAINDER WS-RESTO

           IF WS-RESTO > 0
              ADD 1 TO WS-PRLPAGD
           END-IF

           PERFORM 999-CARREGA-DETALHES
           if ws-fase = '2'
           perform 999-trata-fase2
           end-if
      *    PERFORM 999-CHAMA-FASE2
           .
       220-PF1.
           MOVE '1'                        TO WS-FASE
           PERFORM 999-VALIDA-POSICAO
           MOVE DCLPROD-ID-PRODUTO         TO WS-FASE-COMMAREA
           MOVE 'P'                        TO WS-IDPROD-COMMAREA
      *
      *    EXEC CICS RETURN
      *        PROGRAM('FT6D')
      *        COMMAREA(WS-DFHCOMMAREA-AUX)
      *        LENGTH(LENGTH OF WS-DFHCOMMAREA-AUX)
      *    END-EXEC
      *
           .
       260-PF10.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T08PLOG')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       280-PF11.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T08PLOG')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       290-PF12.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T08PLOG')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       230-PF7.
      *
           IF WS-PRLPAG = 1
              MOVE 'VOCE JA ESTA NA PRIMEIRA PAGINA'
                                          TO PRLMSGO
              PERFORM 999-CARREGA-DETALHES
           ELSE
              SUBTRACT 1 FROM WS-PRLPAG
              PERFORM 999-CARREGA-DETALHES
           END-IF

           PERFORM 999-TRATA-FASE2
           .
      *
       230-PF8.
           MOVE '1'                        TO WS-FASE
      *
           IF WS-PRLPAG = WS-PRLPAGD
              MOVE 'VOCE JA ESTA NA ULTIMA PAGINA'
                                          TO PRLMSGO
              PERFORM 999-CARREGA-DETALHES
           ELSE
              ADD 1 TO WS-PRLPAG
              PERFORM 999-CARREGA-DETALHES
           END-IF

           PERFORM 999-TRATA-FASE2
           .
      *

      *230-PF3.
      *    MOVE 'TERMINO NORMAL DA TRANSCAO' TO WS-MSG-ERRO
      *    PERFORM 999-ENCERRA-TRANSACAO
      *    .
       240-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA'
                                           TO PRLMSGO
           PERFORM 999-TRATA-FASE2
           .
       999-ENCERRA-TRANSACAO.
           MOVE +80                TO WS-LENGTH
           EXEC CICS SEND TEXT
               FROM (WS-MSG-ERRO)
               LENGTH(WS-LENGTH)
               ERASE FREEKB ALARM
           END-EXEC
      *
           EXEC CICS RETURN
           END-EXEC
           .
       999-MANDA-TELA.
           MOVE EIBTRMID                    TO PRLTERO
           MOVE EIBTRNID                    TO PRLTRAO
           MOVE EIBTASKN                    TO PRLTASO
           MOVE WS-FASE                     TO PRLFASO
      *
      *
           EXEC CICS LINK
              PROGRAM('AUXCICS1')
              COMMAREA(WS-VAR-TEMPO)
              LENGTH(+15)
           END-EXEC
           MOVE WS-DATA                    TO PRLDATO
           MOVE WS-HORARIO                 TO PRLHORO
      *
           EXEC CICS SEND
              MAP      ('MAPAPRL')
              MAPSET   ('T08MPRL')
              FROM     (MAPAPRLO)
              ERASE  FREEKB ALARM CURSOR
           END-EXEC
           .
       999-CHAMA-FASE1.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T08PPRL')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       999-CHAMA-FASE2.
           MOVE '2'                        TO WS-FASE
      *
           EXEC CICS RETURN
               TRANSID('FT8C')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
       999-TRATA-FASE2.
           PERFORM 999-PROTECAO-FASE2
           MOVE WS-PRLPAG                  TO PRLPAGO
           MOVE WS-PRLPAGD                 TO PRLPAGDO
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .
      *999-TRATA-FASE3.
      *    MOVE -1                         TO T2CONFL
      *    MOVE 'CONSULTAR OUTRO (S/N)?'   TO T2ACAOO
      *  MOVER OS CAMPOS DO REGISTRO PARA A TELA
      *
      *    PERFORM 999-PROTECAO-FASE3
      *    PERFORM 999-MANDA-TELA
      *    PERFORM 999-CHAMA-FASE3
      *    .
       999-MAPFAIL.
           MOVE 'ERRO MAPA T08PCAD'       TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
       999-ERRO-GENERICO.
           MOVE ws-filtro               TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
       999-NOTFND.
           MOVE 'NAO EXISTEM PRODUTOS COM CODIGO MAIOR OU IGUAL AO INFOR
      -         'MADO'                     TO PRLMSGO
           PERFORM 999-TRATA-FASE2
           .
       999-CARREGA-TS.
           EXEC SQL
               FETCH CURPROD
               INTO  :DCLPROD-NOME-PRODUTO
                    ,:DCLPROD-VENDIDO-POR
                    ,:DCLPROD-VALOR-UNITARIO
           END-EXEC

           EVALUATE SQLCODE
              WHEN 0
                 ADD 1 TO WS-ITEM

                 MOVE DCLPROD-NOME-PRODUTO-text  TO LD-DESCPROD
                 MOVE DCLPROD-VENDIDO-POR-text tO LD-VENDPROD
                 MOVE DCLPROD-VALOR-UNITARIO  TO LD-VALORPROD

                 EXEC CICS WRITEQ TS
                    QUEUE(WS-QUEUE-NAME)
                    FROM(LD-LINDET)
                    ITEM(WS-ITEM)
                    RESP(WS-RESP)
                 END-EXEC

                 IF WS-RESP NOT = DFHRESP(NORMAL)
                    PERFORM 999-ERRO-GENERICO
                 END-IF
              WHEN 100
                 IF WS-ITEM < 1
                    MOVE 'PRODUTO NAO ENCONTRADO' TO PRLMSGO
                    PERFORM 999-TRATA-FASE2
                 END-IF
                 SET ENDFILE-YES TO TRUE
              WHEN OTHER
                 PERFORM 999-ERRO-GENERICO
           END-EVALUATE
           .
       999-CARREGA-DETALHES.
           COMPUTE WS-ITEM = (WS-PRLPAG - 1) * 10 + 1
           MOVE 0                          TO WS-INDEX

           PERFORM WITH TEST AFTER UNTIL WS-INDEX = 10
              EXEC CICS READQ TS
                 QUEUE (WS-QUEUE-NAME)
                 INTO  (LD-LINDET)
                 ITEM  (WS-ITEM)
                 RESP  (WS-RESP)
              END-EXEC

              IF WS-RESP = DFHRESP(NORMAL)
                 ADD 1 TO WS-INDEX
                 MOVE LD-LINDET            TO PRLLINDO(WS-INDEX)
              ELSE
                 IF WS-RESP = DFHRESP(ITEMERR)
                    PERFORM WITH TEST AFTER UNTIL WS-INDEX = 10
                       ADD 1 TO WS-INDEX
                       MOVE SPACES         TO PRLLINDO(WS-INDEX)
                    END-PERFORM
                 ELSE
                    PERFORM 999-ERRO-GENERICO
                 END-IF
              END-IF
              ADD 1 TO WS-ITEM
           END-PERFORM
           .
       999-VALIDA-POSICAO.
           IF EIBCPOSN <= 719 OR EIBCPOSN >= 1520
              MOVE 'POSICAO INVALIDA DO CURSOR'
                                          TO PRLMSGO
              PERFORM 999-TRATA-FASE2
           END-IF
           COMPUTE WS-INDEX = EIBCPOSN / 80 + 1 - 9

           MOVE PRLPRODI                  TO LD-LINDET
           IF DCLPROD-NOME-PRODUTO EQUAL SPACES
              MOVE 'POSICAO NAO CONTEM PRODUTO'
                                         TO PRLMSGO
              PERFORM 999-TRATA-FASE2
           END-IF
           STRING 'PRODUTO'               DELIMITED BY SIZE
                    DCLPROD-NOME-PRODUTO   DELIMITED BY SIZE
                  'SELECIONADO PARA CONSULTA' DELIMITED BY SIZE
                                           INTO PRLMSGO
           PERFORM 999-TRATA-FASE2
           .
       999-PROTECAO-FASE2.
           MOVE DFHUNIMD                   TO PRLPRODA
           MOVE DFHUNIMD                   TO PRLVALA
           MOVE DFHUNIMD                   TO PRLVALTA
           MOVE DFHUNIMD                   TO PRLVENA
           .
