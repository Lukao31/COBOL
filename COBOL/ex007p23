      *                                                                 00060004
      *--------------------------------------------------------------*  00010004
       IDENTIFICATION                      DIVISION.                    00020004
      *--------------------------------------------------------------*  00030004
      *                                                                 00060004
       PROGRAM-ID.                         EX007P23.                    00040008
       AUTHOR.                             LUCAS.                       00050008
      *                                                                 00060004
      *--------------------------------------------------------------*  00070004
       ENVIRONMENT                         DIVISION.                    00080004
      *--------------------------------------------------------------*  00090004
      *                                                                 00060004
       CONFIGURATION                       SECTION.                     00100004
       SPECIAL-NAMES.                                                   00110004
           DECIMAL-POINT IS COMMA.                                      00120004
      *                                                                 00060004
       INPUT-OUTPUT                        SECTION.                     00110004
      *                                                                 00060004
       FILE-CONTROL.
           SELECT MOVCLIDB ASSIGN TO UT-S-MOVCLIDB
           FILE STATUS IS FS-MOVCLIDB
           .
      *
      *--------------------------------------------------------------*  00130004
       DATA                                DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
       FILE                                SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
       FD  MOVCLIDB
           RECORDING MODE IS F
           .
      *
           COPY MOVCLIDB REPLACING ==::== BY ====.
      *--------------------------------------------------------------*  00210008
       WORKING-STORAGE                     SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
      * BOOK DA VARIAVEL ESPELHO                                     *
      *--------------------------------------------------------------*  00210008
           COPY MOVCLIDB REPLACING ==::== BY ==WS-==.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS PARA CALCULO DO TEMPO                     *
      *--------------------------------------------------------------*
           COPY VARDATA.
           COPY VARPROC.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS HOSPEDEIRAS                               *
      *--------------------------------------------------------------*
           EXEC SQL
              INCLUDE BKEXETC
           END-EXEC.

      *
       01  INDICADORES.
           05 ICODCLI                     PIC S9(04) COMP.
           05 INOMECLI                    PIC S9(04) COMP.
           05 IENDCLI                     PIC S9(04) COMP.
           05 IFONECLI                    PIC S9(04) COMP.
           05 IEMAILCLI                   PIC S9(04) COMP.
           05 ICPFCLI                     PIC S9(04) COMP.
           05 ISALDOCLI                   PIC S9(04) COMP.
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS PARA O SQL                                *
      *--------------------------------------------------------------*
           EXEC SQL
             INCLUDE SQLCA
           END-EXEC.
      *
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(05) COMP.
           05 WS-CTINS                     PIC 9(05) COMP.
           05 WS-CTEXC                     PIC 9(05) COMP.
           05 WS-CTINV                     PIC 9(05) COMP.
           05 WS-CTALT                     PIC 9(05) COMP.
           05 WS-CTCON                     PIC 9(05) COMP.
      *
       01  WS-CONTADORES-FORMATADOS.
           05 WS-CTLIDO-F                  PIC ZZ.ZZ9.
           05 WS-CTINS-F                   PIC ZZ.ZZ9.
           05 WS-CTEXC-F                   PIC ZZ.ZZ9.
           05 WS-CTINV-F                   PIC ZZ.ZZ9.
           05 WS-CTALT-F                   PIC ZZ.ZZ9.
           05 WS-CTCON-F                   PIC ZZ.ZZ9.
      *
       01  FS-MOVCLIDB                     PIC X(02).
           88 SUCESSO-MOVCLIDB                     VALUE "00".
           88 FIM-ARQ-MOVCLIDB                     VALUE "10".
      *
       77  WS-SALDOCLI-F                   PIC ZZ.ZZ9,99.
       77  WS-SQLCODE                      PIC +9(9).
       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).
      *
      *--------------------------------------------------------------*  00210008
       PROCEDURE                           DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
       0000-MAIN.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL FIM-ARQ-MOVCLIDB
           PERFORM 3000-TERMINO
           GOBACK
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
      *
           INITIALIZE WS-CONTADORES-COMP
      *
           OPEN INPUT MOVCLIDB
           IF NOT SUCESSO-MOVCLIDB
              MOVE "ERRO DE ABERTURA MOVCLIDB" TO WS-MSG
              MOVE FS-MOVCLIDB                 TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           PERFORM 1100-LER-MOVCLIDB
           .
      *
       1100-LER-MOVCLIDB.
           READ MOVCLIDB INTO WS-REG-MOVCLIDB
           IF SUCESSO-MOVCLIDB
              ADD 1 TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-MOVCLIDB
                    MOVE "ERRO LEITURA MOVCLIDB" TO WS-MSG
                    MOVE FS-MOVCLIDB             TO WS-FS
                    PERFORM 9000-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           EVALUATE WS-TIPOMOVTO-DB
              WHEN "I" PERFORM 2100-INCLUSAO
              WHEN "E" PERFORM 2200-EXCLUSAO
              WHEN "A" PERFORM 2300-ALTERACAO
              WHEN "C" PERFORM 2400-CONSULTAR
              WHEN OTHER
                 ADD 1 TO WS-CTINV
           END-EVALUATE
      *
           PERFORM 1100-LER-MOVCLIDB
           .
      *
       2100-INCLUSAO.
      *----------------------------------------------------------------*
      * MOVER DADOS DA VARIAVEL ESPELHO PARA A HOSPEDEIRA              *
      *----------------------------------------------------------------*
           MOVE WS-CODCLI-DB           TO CODCLI
           MOVE +30                    TO NOMECLI-LEN
           MOVE WS-NOMECLI-DB          TO NOMECLI-TEXT
           MOVE +40                    TO ENDCLI-LEN
           MOVE WS-ENDCLI-DB           TO ENDCLI-TEXT
           MOVE WS-FONECLI-DB          TO FONECLI
           MOVE +50                    TO EMAILCLI-LEN
           MOVE WS-EMAILCLI-DB         TO EMAILCLI-TEXT
           MOVE WS-CPFCLI-DB           TO CPFCLI
           MOVE WS-SALDOCLI-DB         TO SALDOCLI
      *
      *----------------------------------------------------------------*
      * INSERIR OS DADOS DA HOSPEDEIRA NA TABELA (SQL FIELDS)          *
      *----------------------------------------------------------------*
           EXEC SQL
              INSERT INTO TBCLIENT
              (   CODCLI
                , NOMECLI
                , ENDCLI
                , FONECLI
                , EMAILCLI
                , CPFCLI
                , SALDOCLI
              )
              VALUES
              (   :CODCLI
                , :NOMECLI
                , :ENDCLI
                , :FONECLI
                , :EMAILCLI
                , :CPFCLI
                , :SALDOCLI
              )
           END-EXEC

      *
      *----------------------------------------------------------------*
      * TESTAR SQLCODE                                                 *
      *----------------------------------------------------------------*
           EVALUATE SQLCODE
              WHEN 0
                 ADD 1 TO WS-CTINS
              WHEN -803
                 ADD 1 TO WS-CTINV
              WHEN -545
                 ADD 1 TO WS-CTINV
              WHEN OTHER
                 MOVE "ERRO INSERT NA TBCLIENT" TO WS-MSG
                 MOVE SQLCODE                   TO WS-SQLCODE
                 PERFORM 9000-ERRO-DB2
           END-EVALUATE
      *
      *    PERFORM 1100-LER-MOVCLIDB
           .
      *
       2200-EXCLUSAO.
           MOVE WS-CODCLI-DB                 TO CODCLI
      *
           EXEC SQL
              DELETE FROM TBCLIENT
              WHERE CODCLI = :CODCLI
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0    ADD 1    TO WS-CTEXC
              WHEN +100 ADD 1    TO WS-CTINV
              WHEN OTHER
              MOVE "ERRO DELETE NA TABELA TBCLIENT"
                                         TO WS-MSG
              MOVE SQLCODE               TO WS-SQLCODE
              PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       2300-ALTERACAO.
           MOVE WS-CODCLI-DB             TO CODCLI
      *
           EXEC SQL
              SELECT CODCLI
                   , NOMECLI
                   , ENDCLI
                   , FONECLI
                   , EMAILCLI
                   , CPFCLI
                   , SALDOCLI
              INTO   :CODCLI
                   , :NOMECLI
                   , :ENDCLI   :IENDCLI
                   , :FONECLI  :IFONECLI
                   , :EMAILCLI :IEMAILCLI
                   , :CPFCLI   :ICPFCLI
                   , :SALDOCLI :ISALDOCLI
              FROM TBCLIENT
              WHERE CODCLI = :CODCLI
           END-EXEC
      *
           EVALUATE SQLCODE
             WHEN 0
              PERFORM 9000-TRATA-INDICATOR
              PERFORM 2310-UPDATE
             WHEN +100
              ADD 1 TO WS-CTINV
             WHEN OTHER
                MOVE "ERRO SELECT DO UPDATE NA TABELA TBCLIENT"
                                               TO WS-MSG
                MOVE SQLCODE                   TO WS-SQLCODE
                PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       2310-UPDATE.
      * TESTE DA VARIAVEL ESPELHO
           IF WS-NOMECLI-DB  NOT = SPACES
              MOVE +30                         TO NOMECLI-LEN
              MOVE WS-NOMECLI-DB               TO NOMECLI-TEXT
           END-IF
      *
           IF WS-ENDCLI-DB  NOT = SPACES
              MOVE +40                         TO ENDCLI-LEN
              MOVE WS-ENDCLI-DB                TO ENDCLI-TEXT
           END-IF
      *
           IF WS-FONECLI-DB  NOT = SPACES
              MOVE WS-FONECLI-DB               TO FONECLI
           END-IF
      *
           IF WS-EMAILCLI-DB  NOT = SPACES
              MOVE +50                         TO EMAILCLI-LEN
              MOVE WS-EMAILCLI-DB              TO EMAILCLI-TEXT
           END-IF
      *
           IF WS-CPFCLI-DB  NOT = SPACES
              MOVE WS-CPFCLI-DB                TO CPFCLI
           END-IF
      *
           IF WS-SALDOCLI-DB  IS NUMERIC
              MOVE WS-SALDOCLI-DB              TO SALDOCLI
           END-IF
      *
           EXEC SQL
              UPDATE TBCLIENT
              SET    NOMECLI    = :NOMECLI
                   , ENDCLI     = :ENDCLI
                   , FONECLI    = :FONECLI
                   , EMAILCLI   = :EMAILCLI
                   , CPFCLI     = :CPFCLI
                   , SALDOCLI   = :SALDOCLI
              WHERE  CODCLI     = :CODCLI
           END-EXEC

      *
           EVALUATE SQLCODE
              WHEN 0    ADD 1 TO WS-CTALT
              WHEN +100 ADD 1 TO WS-CTINV
              WHEN -545 ADD 1 TO WS-CTINV
              WHEN OTHER
                   MOVE "ERRO UPDATE NA TABELA TBCLIENT"
                                           TO WS-MSG
                   MOVE SQLCODE            TO WS-SQLCODE
                   PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
       2400-CONSULTAR.
           MOVE WS-CODCLI-DB             TO CODCLI
      *
           EXEC SQL
              SELECT CODCLI
                   , NOMECLI
                   , ENDCLI
                   , FONECLI
                   , EMAILCLI
                   , CPFCLI
                   , SALDOCLI
              INTO   :CODCLI
                   , :NOMECLI
                   , :ENDCLI   :IENDCLI
                   , :FONECLI  :IFONECLI
                   , :EMAILCLI :IEMAILCLI
                   , :CPFCLI   :ICPFCLI
                   , :SALDOCLI :ISALDOCLI
              FROM TBCLIENT
              WHERE CODCLI = :CODCLI
           END-EXEC
           .
      *
           EVALUATE SQLCODE
              WHEN 0    ADD 1 TO WS-CTCON
                        PERFORM 9000-TRATA-INDICATOR
                        PERFORM 9000-EXIBE-CONSULTA
              WHEN +100 ADD 1 TO WS-CTINV
              WHEN OTHER
                 MOVE "ERRO CONSULTA NA TABELA TBCLIENT"
                                              TO WS-MSG
                 MOVE SQLCODE                 TO WS-SQLCODE
                 PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .
      *
       3000-TERMINO.
           CLOSE MOVCLIDB
           IF NOT SUCESSO-MOVCLIDB
              MOVE "ERRO FECHAMENTO MOVCLIDB"  TO WS-MSG
              MOVE FS-MOVCLIDB                 TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
      *
           PERFORM 9000-CALCULA-TEMPO-PROC
      *
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTINS                   TO WS-CTINS-F
           MOVE WS-CTEXC                   TO WS-CTEXC-F
           MOVE WS-CTINV                   TO WS-CTINV-F
           MOVE WS-CTCON                   TO WS-CTCON-F
           MOVE WS-CTALT                   TO WS-CTALT-F
      *
           PERFORM 9000-IMPRIME-DATA
      *
      *
           DISPLAY '   '
           DISPLAY '   '
           DISPLAY '=================================================='
           DISPLAY '==  ESTATISTICA FINAL DE PROCESSAMENTO          =='
           DISPLAY '=================================================='
           DISPLAY 'QTDE. DE MOVIMENTOS LIDOS MOVCLIDB.: ' WS-CTLIDO-F
           DISPLAY 'QTDE. CLIENTES INSERIDOS TBCLIENT..: ' WS-CTINS-F
           DISPLAY 'QTDE. CLIENTES EXCLUIDOS...........: ' WS-CTEXC-F
           DISPLAY 'QTDE. CLIENTES ALTERADOS TBCLIENT..: ' WS-CTALT-F
           DISPLAY 'QTDE. CLIENTES CONSULTADOS TBCLIENT: ' WS-CTCON-F
           DISPLAY 'QTDE. MOVIMENTOS INVALIDOS.........: ' WS-CTINV-F
           DISPLAY '=================================================='
           DISPLAY 'TEMPO TOTAL DE PROCESSSAMENTO........: '
                                           WS-TEMPO-PROCESSAMENTO
           DISPLAY '=================================================='
           DISPLAY '==  TERMINO NORMAL DO PROGRAMA EX007P23         =='
           DISPLAY '=================================================='
           .
      *
       9000-TRATA-INDICATOR.
      * TESTE DOS INDICATORS
           IF IENDCLI LESS ZERO
              MOVE SPACES              TO ENDCLI
           END-IF
      *
           IF IFONECLI LESS ZERO
              MOVE SPACES              TO FONECLI
           END-IF
      *
           IF IEMAILCLI LESS ZERO
              MOVE SPACES              TO EMAILCLI
           END-IF
      *
           IF ICPFCLI LESS ZERO
              MOVE SPACES              TO CPFCLI
           END-IF
      *
           IF ISALDOCLI LESS ZERO
              MOVE ZEROS               TO SALDOCLI
           END-IF
           .
      *
       9000-EXIBE-CONSULTA.
           MOVE SALDOCLI                  TO WS-SALDOCLI-F
           DISPLAY '==================================================='
           DISPLAY '==              CONSULTA DE CLIENTE              =='
           DISPLAY '==================================================='
           DISPLAY 'CODIGO.....:' CODCLI
           DISPLAY 'NOME.......:' NOMECLI
           DISPLAY 'ENDERECO...:' ENDCLI
           DISPLAY 'TELEFONE...:' FONECLI
           DISPLAY 'E-MAIL.....:' EMAILCLI-TEXT
           DISPLAY 'CPF........:' CPFCLI
           DISPLAY 'SALDO......: R$ ' WS-SALDOCLI-F
           DISPLAY '==================================================='
           .
      *
       9000-ERRO-DB2.
           DISPLAY '==================================================='
           DISPLAY 'MESSAGE...:' WS-MSG
           DISPLAY 'SQLCODE...:' WS-SQLCODE
           DISPLAY '==================================================='
           DISPLAY '==  TERMINO ANORMAL DO PROGRAMA EX007P23         =='
           DISPLAY '==================================================='
           MOVE 16                         TO RETURN-CODE
           STOP RUN
           .
      *
      *--------------------------------------------------------------*  00210008
      * COPY BOOK DE ROTINAS                                            00210008
      *--------------------------------------------------------------*  00210008
           COPY ROTDATA.
           COPY ROTERRO.
           COPY ROTPROC.
