      *                                                                 00060004
      *--------------------------------------------------------------*  00010004
       IDENTIFICATION                      DIVISION.                    00020004
      *--------------------------------------------------------------*  00030004
      *                                                                 00060004
       PROGRAM-ID.                         EX007P26.                    00040008
       AUTHOR.                             LUCAS.                       00050008
      *                                                                 00060004
      *--------------------------------------------------------------*  00070004
       ENVIRONMENT                         DIVISION.                    00080004
      *--------------------------------------------------------------*  00090004
      *                                                                 00060004
       CONFIGURATION                       SECTION.                     00100004
       SPECIAL-NAMES.                                                   00110004
           DECIMAL-POINT IS COMMA.                                      00120004
      *                                                                 00060004
       INPUT-OUTPUT                        SECTION.                     00110004
      *                                                                 00060004
       FILE-CONTROL.
           SELECT CLIRELAT ASSIGN TO UT-S-CLIRELAT
           FILE STATUS IS FS-CLIRELAT
           .
      *
      *--------------------------------------------------------------*  00130004
       DATA                                DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
       FILE                                SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
       FD  CLIRELAT
           RECORDING MODE IS F
           .
      *
       01  REG-CLIRELAT                    PIC X(132).
      *--------------------------------------------------------------*  00210008
       WORKING-STORAGE                     SECTION.                     00160004
      *--------------------------------------------------------------*
           COPY VARDATA.
           COPY VARPROC.
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS HOSPEDEIRAS                               *
      *--------------------------------------------------------------*
           EXEC SQL
              INCLUDE BKEXETC
           END-EXEC.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS PARA O SQL                                *
      *--------------------------------------------------------------*
           EXEC SQL
             INCLUDE SQLCA
           END-EXEC.
      *
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(06) COMP.
           05 WS-CTIMP                     PIC 9(06) COMP.
           05 WS-CTPAG                     PIC 9(03) COMP.
           05 WS-CTLINHA                   PIC 9(02) COMP.
           05 WS-PULA                      PIC 9(02) COMP.
      *
       01  WS-CONTADORES-FORMATADOS.
           05 WS-CTLIDO-F                  PIC ZZZ.ZZ9.
           05 WS-CTIMP-F                   PIC ZZZ.ZZ9.
           05 WS-CTPAG-F                   PIC ZZ9.
      *
       01  FS-MOVPRDD2                     PIC X(02).
           88 SUCESSO-MOVPRDD2                     VALUE "00".
           88 FIM-ARQ-MOVPRDD2                     VALUE "10".
      *----------------------------------------------------------------*
      * LAYOUT DO RELATORIO CLIRELAT
      *----------------------------------------------------------------*
      * CABECALHO 1
       01  WS-CABEC1.
           05 WS-DATA-CABEC1               PIC X(10).
           05 FILLER                       PIC X(01)
                                                   VALUE SPACES.
           05 WS-HORA-CABEC1               PIC X(08).
           05 FILLER                       PIC X(40)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(30)
                                                   VALUE
           'RELACAO DE CLIENTES SEM E-MAIL'.
           05 FILLER                       PIC X(35)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(05)
                                                   VALUE 'PAG. '.
           05 WS-PAG-CABEC1                PIC ZZ9.
      * CABECALHO 2
       01  WS-CABEC2                       PIC X(132).
      * CABECALHO 3
       01  WS-CABEC3.
           05 FILLER                       PIC X(12)
                                                   VALUE
           ' CODIGO     '.
           05 FILLER                       PIC X(29)
                                                   VALUE
           'NOME                         '.
           05 FILLER                       PIC X(44)
                                                   VALUE
           '     ENDERECO                             '.
           05 FILLER                       PIC X(14)
                                                   VALUE
           '     TELEFONE '.
           05 FILLER                       PIC X(30)
                                                   VALUE
           '     CPF            SALDO     '.
      * CABECALHO 4
       01  WS-CABEC4.
           05 FILLER                       PIC X(33)
                                                   VALUE
           ' ------     ---------------------'.
           05 FILLER                       PIC X(33)
                                                   VALUE
           '---------     -------------------'.
           05 FILLER                       PIC X(33)
                                                   VALUE
           '----------------------     ------'.
           05 FILLER                       PIC X(33)
                                                   VALUE
           '--     -----------      ---------'.
      * LINHA DE DETALHE
       01  WS-LINDET.
           05 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           05 LD-CODCLI                    PIC X(04).
           05 FILLER                       PIC X(06)
                                                   VALUE SPACES.
           05 LD-NOMECLI                   PIC X(30).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.
           05 LD-ENDCLI                    PIC X(40).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.
           05 LD-FONECLI                   PIC X(09).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.
           05 LD-CPFCLI                    PIC X(11).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.
           05 LD-SALDOCLI                  PIC ZZ.ZZ9,99
                                                   BLANK WHEN ZERO.
           05 FILLER                       PIC X(01)
                                                   VALUE SPACES.
      * RODAPE 1
       01  WS-RODAPE1                  PIC X(132)
                                                VALUE ALL '-'.
      * RODAPE 2
       01  WS-RODAPE2.
           05 FILLER                   PIC X(33)
                                               VALUE
           'APOS O USO REUTILIZE ESSE RELATOR'.
           05 FILLER                   PIC X(33)
                                               VALUE
           'IO COMO RASCUNHO - RECICLE SUAS '.
           05 FILLER                   PIC X(33)
                                               VALUE
           'IDEIAS                           '.
           05 FILLER                   PIC X(33)
                                               VALUE
           '  INDUSTRIAS JOAQUIM NABUCO LTDA.'.
      *----------------------------------------------------------------*
      * DATA E HORA PARA O RELATORIO
      *----------------------------------------------------------------*
       01  WS-DATA-SYS.
           05 WS-ANO                   PIC 9(02).
           05 WS-MES                   PIC 9(02).
           05 WS-DIA                   PIC 9(02).
      *
       01  WS-DATA-FORMATADA.
           05 WS-DIA                   PIC 9(02).
           05 FILLER                   PIC X(01)
                                               VALUE '/'.
           05 WS-MES                   PIC 9(02).
           05 FILLER                   PIC X(03)
                                               VALUE '/20'.
           05 WS-ANO                   PIC 9(02).
      *
       01  WS-HORA-SYS.
           05 WS-HORA                  PIC 9(02).
           05 WS-MINUTO                PIC 9(02).
           05 WS-SEGUNDO               PIC 9(02).
      *
       01  WS-HORA-FORMATADA.
           05 WS-HORA                  PIC 9(02).
           05 FILLER                   PIC X(01)
                                               VALUE ':'.
           05 WS-MINUTO                PIC 9(02).
           05 FILLER                   PIC X(01)
                                               VALUE ':'.
           05 WS-SEGUNDO               PIC 9(02).
      *
      * INDICADORES PARA TBCLIENTE
       01  WS-INDICADORES.
           05 IENDCLI                  PIC S9(04) COMP.
           05 IFONECLI                 PIC S9(04) COMP.
           05 ICPFCLI                  PIC S9(04) COMP.
           05 ISALDOCLI                PIC S9(04) COMP.
      *
       01  FS-CLIRELAT                 PIC X(02).
           88 SUCESSO-CLIRELAT                 VALUE '00'.
      *
       77  WS-FS                       PIC X(02).
       77  WS-MSG                      PIC X(60).
       77  WS-SQLCODE                  PIC +9(09).
      *--------------------------------------------------------------*  00210008
       PROCEDURE                           DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
      *0000-MAIN.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL SQLCODE = +100
           PERFORM 3000-TERMINO
           STOP RUN
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
      *
           INITIALIZE WS-CONTADORES-COMP
           MOVE 99                   TO WS-CTLINHA
      *
           ACCEPT WS-DATA-SYS FROM DATE
           ACCEPT WS-DATA-SYS FROM DATE
           MOVE CORRESPONDING WS-DATA-SYS TO WS-DATA-FORMATADA
           MOVE CORRESPONDING WS-DATA-SYS TO WS-DATA-FORMATADA
      *
           EXEC SQL
               DECLARE CLISEMAIL CURSOR FOR
                   SELECT CODCLI
                         ,NOMECLI
                         ,ENDCLI
                         ,FONECLI
                         ,CPFCLI
                         ,SALDOCLI
                   FROM TBCLIENT
                   WHERE EMAILCLI IS NULL
           END-EXEC
      *
           EXEC SQL
                OPEN CLISEMAIL
           END-EXEC
      *
           IF SQLCODE NOT EQUAL 0
              MOVE 'ERRO AO FECHAR CURSOR'
                                        TO WS-MSG
              MOVE SQLCODE              TO WS-SQLCODE
              GO TO 9000-ERRO-DB2
           END-IF
      *

           OPEN OUTPUT CLIRELAT
           IF NOT SUCESSO-CLIRELAT
              MOVE "ERRO DE ABERTURA CLIRELAT" TO WS-MSG
              MOVE FS-CLIRELAT                 TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           PERFORM 1100-LER-CLISEMAIL
           .
      *
       1100-LER-CLISEMAIL.
           EXEC SQL
               FETCH CLISEMAIL
               INTO :CODCLI
                   ,:NOMECLI
                   ,:ENDCLI   :IENDCLI
                   ,:FONECLI  :IFONECLI
                   ,:CPFCLI   :ICPFCLI
                   ,:SALDOCLI :ISALDOCLI
           END-EXEC
      *
           IF SQLCODE = 0
              ADD 1 TO WS-CTLIDO
              PERFORM 1200-TRATA-INDICATOR
           ELSE
              IF SQLCODE NOT = +100
                    MOVE "ERRO LEITURA CURSOR CLISEMAIL"
                                                 TO WS-MSG
                    MOVE SQLCODE                 TO WS-SQLCODE
                    PERFORM 9000-ERRO-DB2
              END-IF
           END-IF
           .
      *
       1200-TRATA-INDICATOR.
           IF IENDCLI < 0
              MOVE ALL '*'             TO ENDCLI-TEXT
           END-IF
      *
           IF IFONECLI < 0
              MOVE ALL '*'             TO FONECLI
           END-IF
      *
           IF ICPFCLI < 0
              MOVE ALL '*'             TO CPFCLI
           END-IF
      *
           IF ISALDOCLI < 0
              MOVE ZERO                TO SALDOCLI
           END-IF
           .
      *
       2000-PROCESSAR.
           IF WS-CTLINHA > 59
              PERFORM 2100-IMPRIME-CABECALHO
           END-IF
      *
           PERFORM 2200-IMPRIME-DETALHE
      *
           IF WS-CTLINHA = 58
              PERFORM 2300-IMPRIME-RODAPE
           END-IF
      *
           PERFORM 1100-LER-CLISEMAIL
           .
      *
       2100-IMPRIME-CABECALHO.
           MOVE WS-DATA-FORMATADA      TO WS-DATA-CABEC1
           MOVE WS-HORA-FORMATADA      TO WS-HORA-CABEC1
           ADD 1 TO WS-CTPAG
           MOVE WS-CTPAG               TO WS-PAG-CABEC1
      *
           WRITE REG-CLIRELAT FROM WS-CABEC1 AFTER PAGE
           IF NOT SUCESSO-CLIRELAT
              MOVE 'ERRO IMPRESSAO CABECALHO 1'
                                       TO WS-MSG
              MOVE FS-CLIRELAT         TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           WRITE REG-CLIRELAT FROM WS-CABEC2
           IF NOT SUCESSO-CLIRELAT
              MOVE 'ERRO IMPRESSAO CABECALHO 2'
                                       TO WS-MSG
              MOVE FS-CLIRELAT         TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           WRITE REG-CLIRELAT FROM WS-CABEC3
           IF NOT SUCESSO-CLIRELAT
              MOVE 'ERRO IMPRESSAO CABECALHO 3'
                                       TO WS-MSG
              MOVE FS-CLIRELAT         TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           WRITE REG-CLIRELAT FROM WS-CABEC4
           IF NOT SUCESSO-CLIRELAT
              MOVE 'ERRO IMPRESSAO CABECALHO 4'
                                       TO WS-MSG
              MOVE FS-CLIRELAT         TO WS-FS
              PERFORM 9000-ERRO
           END-IF
           MOVE 4                      TO WS-CTLINHA
           .
      *
       2200-IMPRIME-DETALHE.
           MOVE CODCLI                 TO LD-CODCLI
           MOVE NOMECLI-TEXT           TO LD-NOMECLI
           MOVE ENDCLI-TEXT            TO LD-ENDCLI
           MOVE FONECLI                TO LD-FONECLI
           MOVE CPFCLI                 TO LD-CPFCLI
           MOVE SALDOCLI               TO LD-SALDOCLI
      *
           WRITE REG-CLIRELAT FROM WS-LINDET
      *
           IF NOT SUCESSO-CLIRELAT
              MOVE 'ERRO NA IMPRESSAO DO DETALHE.'
                                       TO WS-MSG
              MOVE FS-CLIRELAT         TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           ADD 1 TO WS-CTLINHA
           ADD 1 TO WS-CTIMP
           .
      *
       2300-IMPRIME-RODAPE.
           COMPUTE WS-PULA = 59 - WS-CTLINHA
      *
           WRITE REG-CLIRELAT FROM WS-RODAPE1 AFTER WS-PULA LINES
      *
           IF NOT SUCESSO-CLIRELAT
              MOVE 'ERRO NA IMPRESSAO DO RODAPE 1.'
                                       TO WS-MSG
              MOVE FS-CLIRELAT         TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           WRITE REG-CLIRELAT FROM WS-RODAPE2
      *
           IF NOT SUCESSO-CLIRELAT
              MOVE 'ERRO NA IMPRESSAO DO RODAPE 2.'
                                       TO WS-MSG
              MOVE FS-CLIRELAT         TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           MOVE 60                     TO WS-CTLINHA
           .
      *
       3000-TERMINO.
           IF WS-CTLINHA < 60
              PERFORM 2300-IMPRIME-RODAPE
           END-IF
      *
           MOVE WS-CTLIDO              TO WS-CTLIDO-F
           MOVE WS-CTIMP               TO WS-CTIMP-F
           MOVE WS-CTPAG               TO WS-CTPAG-F
      *
           EXEC SQL
              CLOSE CLISEMAIL
           END-EXEC
      *
           IF SQLCODE NOT = 0
              MOVE 'ERRO AO FECHAR CURSOR'
                                       TO WS-MSG
              MOVE SQLCODE             TO WS-SQLCODE
              PERFORM 9000-ERRO-DB2
           END-IF
      *
           CLOSE CLIRELAT
           IF NOT SUCESSO-CLIRELAT
              MOVE "ERRO FECHAMENTO CLIRELAT"  TO WS-MSG
              MOVE FS-CLIRELAT                 TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
      *
           PERFORM 9000-CALCULA-TEMPO-PROC
           PERFORM 9000-IMPRIME-DATA
      *
      *
           DISPLAY '   '
           DISPLAY '   '
           DISPLAY '=================================================='
           DISPLAY '==  ESTATISTICA FINAL DE PROCESSAMENTO          =='
           DISPLAY '=================================================='
           DISPLAY 'TOTAL DE CLIENTES LIDOS.............: ' WS-CTLIDO-F
           DISPLAY 'TOTAL DE CLIENTES IMPRESSOS.........: ' WS-CTIMP-F
           DISPLAY 'TOTAL DE PAGINAS IMPRESSAS..........: ' WS-CTPAG-F
           DISPLAY '=================================================='
           DISPLAY 'TEMPO TOTAL DE PROCESSSAMENTO........: '
                                           WS-TEMPO-PROCESSAMENTO
           DISPLAY '=================================================='
           DISPLAY '==  TERMINO NORMAL DO PROGRAMA EX007P25         =='
           DISPLAY '=================================================='
           .
      *
      *
       9000-ERRO-DB2.
           DISPLAY '==================================================='
           DISPLAY 'MESSAGE...:' WS-MSG
           DISPLAY 'SQLCODE...:' WS-SQLCODE
           DISPLAY '==================================================='
           DISPLAY '==  TERMINO ANORMAL DO PROGRAMA EX007P25         =='
           DISPLAY '==================================================='
           MOVE 16                         TO RETURN-CODE
           STOP RUN
           .
      *
      *--------------------------------------------------------------*  00210008
      * COPY BOOK DE ROTINAS                                            00210008
      *--------------------------------------------------------------*  00210008
           COPY ROTDATA.
           COPY ROTERRO.
           COPY ROTPROC.
