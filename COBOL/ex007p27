      *                                                                 00060004
      *--------------------------------------------------------------*  00010004
       IDENTIFICATION                      DIVISION.                    00020004
      *--------------------------------------------------------------*  00030004
      *                                                                 00060004
       PROGRAM-ID.                         EX007P27.                    00040008
       AUTHOR.                             LUCAS.                       00050008
      *                                                                 00060004
      *--------------------------------------------------------------*  00070004
       ENVIRONMENT                         DIVISION.                    00080004
      *--------------------------------------------------------------*  00090004
      *                                                                 00060004
       CONFIGURATION                       SECTION.                     00100004
       SPECIAL-NAMES.                                                   00110004
           DECIMAL-POINT IS COMMA.                                      00120004
      *                                                                 00060004
       INPUT-OUTPUT                        SECTION.                     00110004
      *                                                                 00060004
       FILE-CONTROL.
           SELECT RELREPO ASSIGN TO UT-S-RELREPO
           FILE STATUS IS FS-RELREPO
           .
      *
      *--------------------------------------------------------------*  00130004
       DATA                                DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
       FILE                                SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
       FD  RELREPO
           RECORDING MODE IS F
           .
      *
       01  REG-RELREPO                     PIC X(80).
      *--------------------------------------------------------------*  00210008
       WORKING-STORAGE                     SECTION.                     00160004
      *--------------------------------------------------------------*
           COPY VARDATA.
           COPY VARPROC.
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS HOSPEDEIRAS                               *
      *--------------------------------------------------------------*
           EXEC SQL
              INCLUDE BK002TP
           END-EXEC.
      *
      *--------------------------------------------------------------*
      * BOOK DAS VARIAVEIS PARA O SQL                                *
      *--------------------------------------------------------------*
           EXEC SQL
             INCLUDE SQLCA
           END-EXEC.
      *
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(06) COMP.
           05 WS-CTIMP                     PIC 9(06) COMP.
           05 WS-CTPAG                     PIC 9(03) COMP.
           05 WS-CTLINHA                   PIC 9(02) COMP.
           05 WS-PULA                      PIC 9(02) COMP.
      *
       01  WS-CONTADORES-FORMATADOS.
           05 WS-CTLIDO-F                  PIC ZZZ.ZZ9.
           05 WS-CTIMP-F                   PIC ZZZ.ZZ9.
           05 WS-CTPAG-F                   PIC ZZ9.
      *
       01  FS-RELREPO                      PIC X(02).
           88 SUCESSO-REL                          VALUE "00".
           88 FIM-ARQ-REL                          VALUE "10".
      *----------------------------------------------------------------*
      * LAYOUT DO RELATORIO RELREPO
      *----------------------------------------------------------------*
      * CABECALHO 1
       01  WS-CABEC1.
           05 WS-DATA-CABEC1               PIC X(10).
           05 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           05 WS-HORA-CABEC1               PIC X(08).
           05 FILLER                       PIC X(15)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(20)
                                                   VALUE
                                         'REPOSICAO DE ESTOQUE'.
           05 FILLER                       PIC X(15)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(05)
                                                   VALUE 'PAG. '.
           05 WS-PAG-CABEC1                PIC Z.ZZ9.
      * CABECALHO 2
       01  WS-CABEC2                       PIC X(80)
                                                   VALUE ALL '-'.
      * CABECALHO 3
       01  WS-CABEC3.
           05 FILLER                       PIC X(20)
                                                   VALUE
           ' CODIGO   DESCRICAO '.
           05 FILLER                       PIC X(22)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(38)
                                                   VALUE
           'PRECO    %LUCRO   ESTOQUE  REPOSICAO  '.

      * CABECALHO 4
       01  WS-CABEC4                       PIC X(80)
                                                   VALUE ALL '-'.
      * LINHA DE DETALHE
       01  WS-LINDET.
           05 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           05 LD-CODPROD                   PIC X(04).
           05 FILLER                       PIC X(04)
                                                   VALUE SPACES.
           05 LD-DESCPROD                  PIC X(20).
           05 FILLER                       PIC X(05)
                                                   VALUE '    R'.
           05 LD-PRECOVENDA                PIC $B***.**9,99.
           05 FILLER                       PIC X(04)
                                                   VALUE SPACES.
           05 LD-LUCRO                     PIC ZZ9,99+.
           05 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           05 LD-QTDEST                    PIC ZZ.ZZ9.
           05 FILLER                       PIC X(06)
                                                   VALUE SPACES.
           05 LD-QTDREP                    PIC ZZ.ZZ9.
           05 FILLER                       PIC X(02)
                                                   VALUE SPACES.
      * RODAPE 1
       01  WS-RODAPE1                  PIC X(80)
                                                VALUE ALL '-'.
      * RODAPE 2
       01  WS-RODAPE2.
           05 FILLER                   PIC X(24)
                                               VALUE
                      'INDUSTRIAS LUCAS L  S/A.'.
           05 FILLER                   PIC X(24)
                                               VALUE SPACES.
           05 FILLER                   PIC X(32)
                                               VALUE
           'SERVIMOS BEM PARA SERVIR SEMPRE.'.
      *----------------------------------------------------------------*
      * DATA E HORA PARA O RELATORIO
      *----------------------------------------------------------------*
       01  WS-DATA-SYS.
           05 WS-ANO                   PIC 9(02).
           05 WS-MES                   PIC 9(02).
           05 WS-DIA                   PIC 9(02).
      *
       01  WS-DATA-FORMATADA.
           05 WS-DIA                   PIC 9(02).
           05 FILLER                   PIC X(01)
                                               VALUE '/'.
           05 WS-MES                   PIC 9(02).
           05 FILLER                   PIC X(03)
                                               VALUE '/20'.
           05 WS-ANO                   PIC 9(02).
      *
       01  WS-HORA-SYS.
           05 WS-HORA                  PIC 9(02).
           05 WS-MINUTO                PIC 9(02).
           05 WS-SEGUNDO               PIC 9(02).
      *
       01  WS-HORA-FORMATADA.
           05 WS-HORA                  PIC 9(02).
           05 FILLER                   PIC X(01)
                                               VALUE ':'.
           05 WS-MINUTO                PIC 9(02).
           05 FILLER                   PIC X(01)
                                               VALUE ':'.
           05 WS-SEGUNDO               PIC 9(02).
      *
      * INDICADORES PARA TBPRODUTO
       01  WS-INDICADORES.
           05 IUNIDPROD                PIC S9(04) COMP.
           05 ILOCALPROD               PIC S9(04) COMP.
           05 IQTDEST                  PIC S9(04) COMP.
           05 IQTDMAX                  PIC S9(04) COMP.
           05 IQTDMIN                  PIC S9(04) COMP.
           05 IPRECOCOMPRA             PIC S9(04) COMP.
           05 IPRECOVENDA              PIC S9(04) COMP.
           05 IPERCOMIS                PIC S9(04) COMP.
      *
       77  WS-FS                       PIC X(02).
       77  WS-MSG                      PIC X(60).
       77  WS-SQLCODE                  PIC +9(09).
      *
       77  WS-LUCRO                    PIC S9(03)V9(6).
       77  WS-QTDREP                   PIC 9(05).
      *--------------------------------------------------------------*  00210008
       PROCEDURE                           DIVISION.                    00140004
      *--------------------------------------------------------------*  00210008
      *0000-MAIN.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL SQLCODE = +100
           PERFORM 3000-TERMINO
           STOP RUN
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
      *
           INITIALIZE WS-CONTADORES-COMP
           MOVE 99                   TO WS-CTLINHA
      *
           OPEN OUTPUT RELREPO
           IF NOT SUCESSO-REL
              MOVE "ERRO DE ABERTURA RELREPO"  TO WS-MSG
              MOVE FS-RELREPO                  TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           EXEC SQL
               DECLARE REPOSICAO CURSOR FOR
                   SELECT CODPROD
                         ,DESCPROD
                         ,QTDEST
                         ,QTDMAX
                         ,PRECOCOMPRA
                         ,PRECOVENDA
                   FROM TBPRODUTO
           END-EXEC
      *
           EXEC SQL
                OPEN REPOSICAO
           END-EXEC
      *
           IF SQLCODE NOT EQUAL 0
              MOVE 'ERRO AO FECHAR CURSOR'
                                        TO WS-MSG
              MOVE SQLCODE              TO WS-SQLCODE
              GO TO 9000-ERRO-DB2
           END-IF
      *
           ACCEPT WS-DATA-SYS FROM DATE
           ACCEPT WS-DATA-SYS FROM DATE
           MOVE CORRESPONDING WS-DATA-SYS TO WS-DATA-FORMATADA
           MOVE CORRESPONDING WS-DATA-SYS TO WS-DATA-FORMATADA

      *
           PERFORM 1100-LER-REPOSICAO
           .
      *
       1100-LER-REPOSICAO.
           EXEC SQL
               FETCH REPOSICAO
               INTO :CODPROD
                   ,:DESCPROD
                   ,:QTDEST      :IQTDEST
                   ,:QTDMAX      :IQTDMAX
                   ,:PRECOCOMPRA :IPRECOCOMPRA
                   ,:PRECOVENDA  :IPRECOVENDA
           END-EXEC
      *
           IF SQLCODE = 0
              ADD 1 TO WS-CTLIDO
              PERFORM 1200-TRATA-INDICATOR
           ELSE
              IF SQLCODE NOT = +100
                    MOVE "ERRO LEITURA CURSOR REPOSICAO"
                                                 TO WS-MSG
                    MOVE SQLCODE                 TO WS-SQLCODE
                    PERFORM 9000-ERRO-DB2
              END-IF
           END-IF
           .
      *
       1200-TRATA-INDICATOR.
           IF IQTDEST < 0
              MOVE ZEROS               TO QTDEST
           END-IF
      *
           IF IQTDMAX < 0
              MOVE ZEROS               TO QTDMAX
           END-IF
      *
           IF IPRECOCOMPRA < 0
              MOVE ZEROS               TO PRECOCOMPRA
           END-IF
      *
           IF IPRECOVENDA < 0
              MOVE ZERO                TO PRECOVENDA
           END-IF
           .
      *
       2000-PROCESSAR.
           IF WS-CTLINHA > 59
              PERFORM 2100-IMPRIME-CABECALHO
           END-IF
      *
           PERFORM 2200-IMPRIME-DETALHE
      *
           IF WS-CTLINHA = 58
              PERFORM 2300-IMPRIME-RODAPE
           END-IF
      *
           PERFORM 1100-LER-REPOSICAO
           .
      *
       2100-IMPRIME-CABECALHO.
           MOVE WS-DATA-FORMATADA      TO WS-DATA-CABEC1
           MOVE WS-HORA-FORMATADA      TO WS-HORA-CABEC1
           ADD 1 TO WS-CTPAG
           MOVE WS-CTPAG               TO WS-PAG-CABEC1
      *
           WRITE REG-RELREPO FROM WS-CABEC1 AFTER PAGE
           IF NOT SUCESSO-REL
              MOVE 'ERRO IMPRESSAO CABECALHO 1'
                                       TO WS-MSG
              MOVE FS-RELREPO          TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           WRITE REG-RELREPO FROM WS-CABEC2
           IF NOT SUCESSO-REL
              MOVE 'ERRO IMPRESSAO CABECALHO 2'
                                       TO WS-MSG
              MOVE FS-RELREPO          TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           WRITE REG-RELREPO FROM WS-CABEC3
           IF NOT SUCESSO-REL
              MOVE 'ERRO IMPRESSAO CABECALHO 3'
                                       TO WS-MSG
              MOVE FS-RELREPO          TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           WRITE REG-RELREPO FROM WS-CABEC4
           IF NOT SUCESSO-REL
              MOVE 'ERRO IMPRESSAO CABECALHO 4'
                                       TO WS-MSG
              MOVE FS-RELREPO          TO WS-FS
              PERFORM 9000-ERRO
           END-IF
           MOVE 4                      TO WS-CTLINHA
           .
      *
       2200-IMPRIME-DETALHE.
           MOVE CODPROD                TO LD-CODPROD
           MOVE DESCPROD-TEXT          TO LD-DESCPROD
           MOVE PRECOVENDA             TO LD-PRECOVENDA
           MOVE QTDEST                 TO LD-QTDEST
      *
           IF PRECOCOMPRA = 0
              MOVE 0                   TO WS-LUCRO
           ELSE
              COMPUTE WS-LUCRO = (PRECOVENDA / PRECOCOMPRA - 1) * 100
           END-IF
      *
           COMPUTE WS-QTDREP = QTDMAX - QTDEST
           MOVE WS-LUCRO               TO LD-LUCRO
           MOVE WS-QTDREP              TO LD-QTDREP
      *
           WRITE REG-RELREPO FROM WS-LINDET
      *
           IF NOT SUCESSO-REL
              MOVE 'ERRO NA IMPRESSAO DO DETALHE.'
                                       TO WS-MSG
              MOVE FS-RELREPO          TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           ADD 1 TO WS-CTLINHA
           ADD 1 TO WS-CTIMP
           .
      *
       2300-IMPRIME-RODAPE.
           COMPUTE WS-PULA = 59 - WS-CTLINHA
      *
           WRITE REG-RELREPO FROM WS-RODAPE1 AFTER WS-PULA LINES
      *
           IF NOT SUCESSO-REL
              MOVE 'ERRO NA IMPRESSAO DO RODAPE 1.'
                                       TO WS-MSG
              MOVE FS-RELREPO          TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           WRITE REG-RELREPO FROM WS-RODAPE2
      *
           IF NOT SUCESSO-REL
              MOVE 'ERRO NA IMPRESSAO DO RODAPE 2.'
                                       TO WS-MSG
              MOVE FS-RELREPO          TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           MOVE 60                     TO WS-CTLINHA
           .
      *
       3000-TERMINO.
           IF WS-CTLINHA < 60
              PERFORM 2300-IMPRIME-RODAPE
           END-IF
      *
           MOVE WS-CTLIDO              TO WS-CTLIDO-F
           MOVE WS-CTIMP               TO WS-CTIMP-F
           MOVE WS-CTPAG               TO WS-CTPAG-F
      *
           EXEC SQL
              CLOSE REPOSICAO
           END-EXEC
      *
           IF SQLCODE NOT = 0
              MOVE 'ERRO AO FECHAR CURSOR'
                                       TO WS-MSG
              MOVE SQLCODE             TO WS-SQLCODE
              PERFORM 9000-ERRO-DB2
           END-IF
      *
           CLOSE RELREPO
           IF NOT SUCESSO-REL
              MOVE "ERRO FECHAMENTO RELREPO"   TO WS-MSG
              MOVE FS-RELREPO                  TO WS-FS
              PERFORM 9000-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
      *
           PERFORM 9000-CALCULA-TEMPO-PROC
           PERFORM 9000-IMPRIME-DATA
      *
           DISPLAY '   '
           DISPLAY '   '
           DISPLAY '=================================================='
           DISPLAY '==  ESTATISTICA FINAL DE PROCESSAMENTO          =='
           DISPLAY '=================================================='
           DISPLAY 'TOTAL DE PRODUTOS LIDOS.............: ' WS-CTLIDO-F
           DISPLAY 'TOTAL DE LINHASS IMPRESSAS..........: ' WS-CTIMP-F
           DISPLAY 'TOTAL DE PAGINAS IMPRESSAS..........: ' WS-CTPAG-F
           DISPLAY '=================================================='
           DISPLAY 'TEMPO TOTAL DE PROCESSSAMENTO........: '
                                           WS-TEMPO-PROCESSAMENTO
           DISPLAY '=================================================='
           DISPLAY '==  TERMINO NORMAL DO PROGRAMA EX007P27         =='
           DISPLAY '=================================================='
           .
      *
       9000-ERRO-DB2.
           DISPLAY '==================================================='
           DISPLAY 'MESSAGE...:' WS-MSG
           DISPLAY 'SQLCODE...:' WS-SQLCODE
           DISPLAY '==================================================='
           DISPLAY '==  TERMINO ANORMAL DO PROGRAMA EX007P27         =='
           DISPLAY '==================================================='
           MOVE 16                         TO RETURN-CODE
           STOP RUN
           .
      *
      *--------------------------------------------------------------*  00210008
      * COPY BOOK DE ROTINAS                                            00210008
      *--------------------------------------------------------------*  00210008
           COPY ROTDATA.
           COPY ROTERRO.
           COPY ROTPROC.
