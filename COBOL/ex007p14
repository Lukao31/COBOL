      *--------------------------------------------------------------*  00010004
       IDENTIFICATION                      DIVISION.                    00020004
      *--------------------------------------------------------------*  00030004
       PROGRAM-ID.                         EX007P14.                    00040008
       AUTHOR.                             LUCAS.                       00050008
      *                                                                 00060004
      *--------------------------------------------------------------*  00070004
       ENVIRONMENT                         DIVISION.                    00080004
      *--------------------------------------------------------------*  00090004
       CONFIGURATION                       SECTION.                     00100004
       SPECIAL-NAMES.                                                   00110004
           DECIMAL-POINT IS COMMA.                                      00120004
      *
       INPUT-OUTPUT                        SECTION.                     00100004
      *
       FILE-CONTROL.
           SELECT CLIOLD ASSIGN TO UT-S-CLIOLD
           FILE STATUS IS FS-CLIOLD
           .
           SELECT CLIMOV ASSIGN TO UT-S-CLIMOV
           FILE STATUS IS FS-CLIMOV
           .
           SELECT CLINEW ASSIGN TO UT-S-CLINEW
           FILE STATUS IS FS-CLINEW
           .
      *--------------------------------------------------------------*  00130004
       DATA                                DIVISION.                    00140004
      *--------------------------------------------------------------*  00150004
       FILE                                SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
       FD  CLIOLD
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 79 CHARACTERS
           DATA RECORD IS REG-CLIOLD
           .
      *
       01  REG-CLIOLD                      PIC X(79).
      *
       FD  CLIMOV
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 80 CHARACTERS
           DATA RECORD IS REG-CLIMOV
           .
      *
       01  REG-CLIMOV                      PIC X(80).
      *
       FD  CLINEW
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 79 CHARACTERS
           DATA RECORD IS REG-CLINEW
           .
      *
       01  REG-CLINEW                      PIC X(79).
      *--------------------------------------------------------------*  00210008
       WORKING-STORAGE                     SECTION.                     00160004
      *--------------------------------------------------------------*  00210008
      * FLAGS PARA O FILE STATUS DOS ARQUIVOS                        *
      *--------------------------------------------------------------*  00210008
       01  FS-CLIOLD                       PIC X(02).                   00210108
           88 SUCESSO-OLD                          VALUE "00".
           88 FIM-ARQ-OLD                          VALUE "10".
      *
       01  FS-CLIMOV                       PIC X(02).                   00210108
           88 SUCESSO-MOV                          VALUE "00".
           88 FIM-ARQ-MOV                          VALUE "10".
      *
       01  FS-CLINEW                       PIC X(02).
           88 SUCESSO-NEW                          VALUE "00".
           88 FIM-ARQ-NEW                          VALUE "10".
      *--------------------------------------------------------------*  00210008
      * VARIAVEIS PARA A ROTINA DE ERRO                              *
      *--------------------------------------------------------------*  00210008
       77  WS-MSG                          PIC X(60).                   00210208
       77  WS-FS                           PIC X(02).                   00210308
      *--------------------------------------------------------------*
      * CONTADORES NO FORMATO BINARIO                                *
      *--------------------------------------------------------------*
       77  WS-CTLIDO-OLD                   PIC 9(04) COMP.              00210308
       77  WS-CTLIDO-MOV                   PIC 9(04) COMP.              00210308
       77  WS-CTGRAV-NEW                   PIC 9(04) COMP.              00210308
       77  WS-CTINC                        PIC 9(04) COMP.
       77  WS-CTEXC                        PIC 9(04) COMP.
       77  WS-CTALT                        PIC 9(04) COMP.
       77  WS-CTINV                        PIC 9(04) COMP.
       77  WS-CTMOV-INV                    PIC 9(04) COMP.
      *--------------------------------------------------------------*  00210008
      * VARIAVEIS COM MASCARA DE EDICAO (FORMATADAS)                 *
      *--------------------------------------------------------------*  00210008
       77  WS-CTLIDO-OLD-F                 PIC ZZZ9.                    00210308
       77  WS-CTLIDO-MOV-F                 PIC ZZZ9.                    00210308
       77  WS-CTGRAV-NEW-F                 PIC ZZZ9.                    00210308
       77  WS-CTINC-F                      PIC ZZZ9.
       77  WS-CTEXC-F                      PIC ZZZ9.
       77  WS-CTALT-F                      PIC ZZZ9.
       77  WS-CTINV-F                      PIC ZZZ9.
       77  WS-CTMOV-INV-F                  PIC ZZZ9.
      *--------------------------------------------------------------*  00210008
      * DEFINICAO DAS VARIAVEIS ESPELHOS                            *
      *--------------------------------------------------------------*  00210008
       01  WS-REG-CLIOLD.
           05 WS-CODCLI-O                  PIC X(04).
           05 WS-NOMECLI-O                 PIC X(25).
           05 WS-ENDCLI-O                  PIC X(30).
           05 WS-FONECLI-O                 PIC X(10).
           05 WS-TOTALDIVIDA-O             PIC 9(08)V99.
      *--------------------------------------------------------------*
       01  WS-REG-CLIMOV.
           05 WS-CODCLI-M                  PIC X(04).
           05 WS-NOMECLI-M                 PIC X(25).
           05 WS-ENDCLI-M                  PIC X(30).
           05 WS-FONECLI-M                 PIC X(10).
           05 WS-TOTALDIVIDA-M             PIC 9(08)V99.
           05 WS-TIPOMOV-M                 PIC X(1).
      *--------------------------------------------------------------*
       01  WS-REG-CLINEW.
           05 WS-CODCLI-N                  PIC X(04).
           05 WS-NOMECLI-N                 PIC X(25).
           05 WS-ENDCLI-N                  PIC X(30).
           05 WS-FONECLI-N                 PIC X(10).
           05 WS-TOTALDIVIDA-N             PIC 9(08)V99.
      *--------------------------------------------------------------*  00211007
       01  WS-PROGRAMA                     PIC X(08)
                                                   VALUE "PGMAUX02".
       01  WS-DADOS-ENVIADOS.
           05 WS-TOTALDIVIDA               PIC 9(08)V99.
           05 WS-RESP                      PIC X(01).
           05 WS-DIVIDACALC                PIC 9(08)V99.
      *--------------------------------------------------------------*  00211007
           COPY VARDATA.
           COPY VARPROC.
      *--------------------------------------------------------------*  00211007
       PROCEDURE                           DIVISION.                    00220004
      *--------------------------------------------------------------*  00230004
       0000-EX007P14.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR UNTIL FIM-ARQ-OLD
                                    AND FIM-ARQ-MOV
           PERFORM 3000-TERMINO
           GOBACK
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL       FROM TIME
           MOVE 0                          TO WS-CTLIDO-OLD
                                              WS-CTLIDO-MOV
                                              WS-CTGRAV-NEW
                                              WS-CTINC
                                              WS-CTEXC
                                              WS-CTALT
                                              WS-CTINV
                                              WS-CTMOV-INV
           OPEN INPUT CLIOLD
      *
           IF NOT SUCESSO-OLD
              MOVE "ERRO ABERTURA CLIOLD"  TO WS-MSG
              MOVE FS-CLIOLD               TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *
           OPEN INPUT CLIMOV
      *
           IF NOT SUCESSO-MOV
              MOVE "ERRO ABERTURA CLIMOV"  TO WS-MSG
              MOVE FS-CLIMOV               TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *
           OPEN OUTPUT CLINEW
      *
           IF NOT SUCESSO-NEW
              MOVE "ERRO ABERTURA CLINEW"  TO WS-MSG
              MOVE FS-CLINEW               TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *
           PERFORM 1100-LER-CLIOLD
      *
           IF FIM-ARQ-OLD
              MOVE "CLIOLD VAZIO"          TO WS-MSG
              MOVE FS-CLIOLD               TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *
           PERFORM 1200-LER-CLIMOV
      *
           IF FIM-ARQ-MOV
              MOVE "CLIMOV VAZIO"          TO WS-MSG
              MOVE FS-CLIMOV               TO WS-FS
              GO TO 9000-ERRO
           END-IF
           .
      *
       1100-LER-CLIOLD.
           READ CLIOLD INTO WS-REG-CLIOLD
      *
           IF SUCESSO-OLD
              ADD 1 TO WS-CTLIDO-OLD
           ELSE
              IF FIM-ARQ-OLD
                 MOVE HIGH-VALUES          TO WS-CODCLI-O
              ELSE
                MOVE "ERRO LEITURA CLIOLD" TO WS-MSG
                MOVE FS-CLIOLD             TO WS-FS
                 GO TO 9000-ERRO
              END-IF
           END-IF
           .
      *
       1200-LER-CLIMOV.
           READ CLIMOV INTO WS-REG-CLIMOV

           IF SUCESSO-MOV
              ADD 1 TO WS-CTLIDO-MOV
           ELSE
              IF FIM-ARQ-MOV
                 MOVE HIGH-VALUES          TO WS-CODCLI-M
              ELSE
                MOVE "ERRO LEITURA CLIMOV" TO WS-MSG
                MOVE FS-CLIMOV             TO WS-FS
                 GO TO 9000-ERRO
              END-IF
           END-IF
           .
      *
       2000-PROCESSAR.
           IF WS-CODCLI-O < WS-CODCLI-M
              ADD 1 TO WS-CTINV
              PERFORM 1100-LER-CLIOLD
           ELSE
              IF WS-CODCLI-O > WS-CODCLI-M
                 PERFORM 2100-INCLUIR
                 PERFORM 1200-LER-CLIMOV
              ELSE
                 PERFORM 2200-ALT-EXC
                 PERFORM 1100-LER-CLIOLD
                 PERFORM 1200-LER-CLIMOV
              END-IF
           END-IF
           .
      *
       2100-INCLUIR.
           IF WS-TIPOMOV-M = "I"
           PERFORM 2400-GRAVA-M
           ADD 1                           TO WS-CTINC
           ELSE
              ADD 1                        TO WS-CTMOV-INV
           END-IF
           .
      *
       2200-ALT-EXC.
           IF WS-TIPOMOV-M = "A"
              PERFORM 2300-ALTERAR
           ELSE
              IF WS-TIPOMOV-M = "E"
              ADD 1                        TO WS-CTEXC
              ELSE
                 ADD 1                     TO WS-CTMOV-INV
              END-IF
           END-IF
           .
      *
       2300-ALTERAR.
           MOVE WS-TOTALDIVIDA-O          TO WS-TOTALDIVIDA
           CALL WS-PROGRAMA USING WS-DADOS-ENVIADOS
           CANCEL WS-PROGRAMA
           IF WS-RESP = "0"
              MOVE WS-DIVIDACALC          TO WS-TOTALDIVIDA-O
              ADD 1                       TO WS-CTALT
              PERFORM 2400-GRAVA-M
              MOVE SPACES                 TO WS-RESP
           ELSE
              ADD 1                       TO WS-CTMOV-INV
              ADD 1                       TO WS-CTINV
           END-IF
           .
      *
       2400-GRAVA-M.
           MOVE WS-CODCLI-M                TO WS-CODCLI-N
      *
           IF WS-NOMECLI-M NOT = SPACES
              MOVE WS-NOMECLI-M            TO WS-NOMECLI-N
           ELSE
              MOVE WS-NOMECLI-O            TO WS-NOMECLI-N
           END-IF
      *
           IF WS-ENDCLI-M NOT = SPACES
              MOVE WS-ENDCLI-M             TO WS-ENDCLI-N
           ELSE
              MOVE WS-ENDCLI-O             TO WS-ENDCLI-N
           END-IF
      *
           IF WS-FONECLI-M NOT = SPACES
              MOVE WS-FONECLI-M            TO WS-FONECLI-N
           ELSE
              MOVE WS-FONECLI-O            TO WS-FONECLI-N
           END-IF
      *
           IF WS-TOTALDIVIDA-M IS NUMERIC
              MOVE WS-TOTALDIVIDA-M        TO WS-TOTALDIVIDA-N
           ELSE
              IF WS-RESP NOT = "0"
                 MOVE WS-TOTALDIVIDA-M     TO WS-TOTALDIVIDA-N
              END-IF
           END-IF
      *
           WRITE REG-CLINEW                FROM WS-REG-CLINEW
      *
           IF NOT SUCESSO-NEW
             MOVE "ERRO NA GRAVACAO CLINEW"  TO WS-MSG
             MOVE FS-CLINEW                  TO WS-FS
             GO TO 9000-ERRO
           END-IF
      *
           ADD 1                           TO WS-CTGRAV-NEW
           .
      *
       3000-TERMINO.
           PERFORM 9000-IMPRIME-DATA
      *
           CLOSE CLIOLD
           IF NOT SUCESSO-OLD
             MOVE "ERRO FECHAMENTO CLIOLD"     TO WS-MSG
             MOVE FS-CLIOLD                    TO WS-FS
             GO TO 9000-ERRO
           END-IF
      *
           CLOSE CLIMOV
           IF NOT SUCESSO-MOV
             MOVE "ERRO FECHAMENTO CLIOLD"   TO WS-MSG
             MOVE FS-CLIMOV                  TO WS-FS
             GO TO 9000-ERRO
           END-IF
      *
           CLOSE CLINEW
           IF NOT SUCESSO-NEW
             MOVE "ERRO FECHAMENTO CLINEW"   TO WS-MSG
             MOVE FS-CLINEW                  TO WS-FS
             GO TO 9000-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL         FROM TIME
           PERFORM 9000-CALCULA-TEMPO-PROC
           MOVE WS-CTLIDO-OLD              TO WS-CTLIDO-OLD-F
           MOVE WS-CTLIDO-MOV              TO WS-CTLIDO-MOV-F
           MOVE WS-CTGRAV-NEW              TO WS-CTGRAV-NEW-F
           MOVE WS-CTINC                   TO WS-CTINC-F
           MOVE WS-CTEXC                   TO WS-CTEXC-F
           MOVE WS-CTALT                   TO WS-CTALT-F
           MOVE WS-CTINV                   TO WS-CTINV-F
           MOVE WS-CTMOV-INV               TO WS-CTMOV-INV-F
      *
           DISPLAY '=================================================='
           DISPLAY 'TOTAL DE REGISTROS LIDOS OLD..: ' WS-CTLIDO-OLD-F
           DISPLAY 'TOTAL DE REGISTROS LIDOS MOV..: ' WS-CTLIDO-MOV-F
           DISPLAY 'TOTAL DE REGISTROS GRAV. NEW..: ' WS-CTGRAV-NEW-F
           DISPLAY 'TOTAL DE REGISTROS INCLUIDOS..: ' WS-CTINC-F
           DISPLAY 'TOTAL DE REGISTROS EXCLUIDOS..: ' WS-CTEXC-F
           DISPLAY 'TOTAL DE REGISTROS ALTERADOS..: ' WS-CTALT-F
           DISPLAY 'TOTAL DE REGISTROS INVALIDOS..: ' WS-CTINV-F
           DISPLAY 'TOTAL DE REGISTROS MOV. INVA..: ' WS-CTMOV-INV-F
           DISPLAY '=================================================='
           DISPLAY 'TEMPO DE PROCESSAMENTO.: ' WS-TEMPO-PROCESSAMENTO-F
           DISPLAY '=================================================='
           DISPLAY '             TERMINO NORMAL EX007P13              '
           DISPLAY '=================================================='
           .
      *
           COPY ROTDATA.
           COPY ROTPROC2.
           COPY ROTERRO.
